#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Элементы.РуководствоПользователяБаннер.Видимость = НастроитьРуководствоПользователяБаннер();
	
	Если Параметры.Ключ.Пустая() Тогда
		НастроитьОтображениеСодержимогоДокумента();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	НастроитьОтображениеСодержимогоДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеУчетнойПолитики" И Параметр = Объект.Организация Тогда
		НастроитьОтображениеСодержимогоДокумента();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не КоманднаяПанель.Видимость Тогда
		Модифицированность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	НастроитьОтображениеСодержимогоДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	НастроитьОтображениеСодержимогоДокумента();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РуководствоПользователяЗакрытьНажатие(Элемент)
	
	Элементы.РуководствоПользователяБаннер.Видимость = НастроитьРуководствоПользователяБаннер(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьЗагрузкуИзФайла(Команда)
	
	ПараметрыЗагрузки = ЗагрузкаДанныхИзВнешнихФайловКлиент.НовыйПараметрыЗагрузкиВТабЧасть();
	
	ПараметрыЗагрузки.ИмяТабличнойЧасти = "Документ.ОбесценениеЗапасов.Запасы";
	ПараметрыЗагрузки.Заголовок = НСтр("ru = 'Загрузка перечня запасов, имеющих признаки обесценения'");
	ПараметрыЗагрузки.Оповещение = Новый ОписаниеОповещения("ЗавершитьЗагрузкуИзФайла", ЭтотОбъект);
	ПараметрыЗагрузки.Пояснение = Нстр("ru = 'Укажите названия реквизитов в заголовках колонок, которые хотите загрузить. <span style=""color: НезаполненныйРеквизит"">Ненайденная</span> номенклатура не будет загружена.'");
		
	РеквизитыДляСопоставления = Новый СписокЗначений;
	РеквизитыДляСопоставления.Добавить("Номенклатура_Артикул");
	РеквизитыДляСопоставления.Добавить("Номенклатура_Код");
	РеквизитыДляСопоставления.Добавить("Номенклатура_Наименование");
	
	ПараметрыЗагрузки.РеквизитыДляСопоставления = ПоместитьВоВременноеХранилище(
		РеквизитыДляСопоставления, УникальныйИдентификатор);

	ЗагрузкаДанныхИзВнешнихФайловКлиент.ВыбратьФайлДляЗагрузки(
		"Обработка.ЗагрузкаДанныхВТабличнуюЧасть.Форма",
		УникальныйИдентификатор,
		ПараметрыЗагрузки);
	
КонецПроцедуры

#Область СтандартныеПодсистемы_ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗавершитьЗагрузкуИзФайла(АдресЗагруженныхДанных, НеиспользуемыйПараметр) Экспорт
	
	Если АдресЗагруженныхДанных = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ЗаполнитьТабличнуюЧасть(АдресЗагруженныхДанных);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТабличнуюЧасть(АдресЗагруженныхДанных)
	
	ЗагруженныеДанные = ПолучитьИзВременногоХранилища(АдресЗагруженныхДанных);
	Объект.Запасы.Очистить();
	
	Для Каждого ЗагруженнаяСтрока Из ЗагруженныеДанные Цикл 
	
		НоваяСтрока = Объект.Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ЗагруженнаяСтрока);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьОтображениеСодержимогоДокумента()
	
	ВидимостьКоманд = Истина;
	Страница = Элементы.ОбесценениеЗапасов;
	
	Если Не УчетнаяПолитика.ОбесценениеЗапасов(Объект.Организация, Объект.Дата) Тогда
		ВидимостьКоманд = Ложь;
		Страница = Элементы.ИспользованиеОтключено;
	КонецЕсли;
	
	Элементы.СодержимоеДокумента.ТекущаяСтраница = Страница;
	КоманднаяПанель.Видимость = ВидимостьКоманд;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НастроитьРуководствоПользователяБаннер(Знач НовоеЗначение = Неопределено)
	
	Если НовоеЗначение <> Неопределено Тогда
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"РуководствоПользователяВидимость",
			"ОбесценениеЗапасов",
			НовоеЗначение);
	КонецЕсли;
	
	Возврат ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"РуководствоПользователяВидимость",
		"ОбесценениеЗапасов",
		Истина);
	
КонецФункции

#КонецОбласти
