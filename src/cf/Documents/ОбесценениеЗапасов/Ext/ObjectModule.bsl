#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПометкаУдаления И Не УчетнаяПолитика.ОбесценениеЗапасов(Организация, Дата) Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Учет обесценения запасов отключен в учетной политике'"),
			Ссылка,
			"Дата", // Отправляем в то поле, который не скрывается функциональностью
			"Дата",
			Отказ);
	КонецЕсли;
	
	ПроверитьДокументЗаЭтуДатуЕдинственный(Отказ, РежимЗаписи);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	РаботаСПоследовательностями.ЗарегистрироватьУстареваниеОперации(
		ЭтотОбъект,
		Перечисления.ВидыРегламентныхОпераций.ОбесценениеЗапасов);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	РаботаСПоследовательностями.ЗарегистрироватьУстареваниеОперации(
		ЭтотОбъект,
		Перечисления.ВидыРегламентныхОпераций.ОбесценениеЗапасов);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьДокументЗаЭтуДатуЕдинственный(Отказ, РежимЗаписи)

	Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Проведен И РежимЗаписи <> РежимЗаписиДокумента.Проведение Тогда
		Возврат;
	КонецЕсли;
	
	Диапазон = Новый Диапазон(НачалоДня(Дата), КонецДня(Дата));
	
	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("Документ.ОбесценениеЗапасов");
	ЭлементБлокировкиДанных.УстановитьЗначение("Дата", Диапазон);
	
	БлокировкаДанных.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",        Ссылка);
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("НачалоПериода", Диапазон.Начало);
	Запрос.УстановитьПараметр("КонецПериода",  Диапазон.Конец);
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОбесценениеЗапасов.Ссылка КАК Ссылка,
	|	ОбесценениеЗапасов.Дата КАК Дата
	|ИЗ
	|	Документ.ОбесценениеЗапасов КАК ОбесценениеЗапасов
	|ГДЕ
	|	ОбесценениеЗапасов.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ОбесценениеЗапасов.Проведен
	|	И ОбесценениеЗапасов.Организация = &Организация
	|	И ОбесценениеЗапасов.Ссылка <> &Ссылка";
		
	Если Запрос.Выполнить().Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Сообщение = СтрШаблон(
		НСтр("ru = 'На %1 уже введен перечень товаров, имеющих признаки обесценения'"),
		Формат(Дата, "ДЛФ=D"));
		
	ОбщегоНазначения.СообщитьПользователю(
		Сообщение,
		Ссылка,
		"Дата",
		"Дата",
		Отказ);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

