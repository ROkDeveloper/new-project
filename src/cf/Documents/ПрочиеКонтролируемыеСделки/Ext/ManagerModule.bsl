#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета",
		ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета());
	
	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = ТекстЗапросаКонтролируемыхСделокОрганизаций(НомераТаблиц);
		
	Результат = Запрос.ВыполнитьПакет();
	
	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
	
КонецФункции

Функция ТекстЗапросаКонтролируемыхСделокОрганизаций(НомераТаблиц)
	
	НомераТаблиц.Вставить("КонтролируемыеСделкиОрганизаций", НомераТаблиц.Количество());
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПрочиеКонтролируемыеСделкиСделки.Ссылка КАК Регистратор,
	|	ПрочиеКонтролируемыеСделкиСделки.ДатаСовершенияСделки КАК Период,
	|	ПрочиеКонтролируемыеСделки.УведомлениеОКонтролируемойСделке КАК Уведомление,
	|	ПрочиеКонтролируемыеСделки.Организация КАК Организация,
	|	ПрочиеКонтролируемыеСделки.Контрагент КАК Контрагент,
	|	ПрочиеКонтролируемыеСделки.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ПрочиеКонтролируемыеСделкиСделки.ПредметСделки КАК ПредметСделки,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперацииКонтролируемыхСделок.ПрочаяОперация) КАК ХозяйственнаяОперация,
	|	ПрочиеКонтролируемыеСделкиСделки.Ссылка КАК РасчетныйДокумент,
	|	ПрочиеКонтролируемыеСделки.ВалютаДокумента КАК Валюта,
	|	ПрочиеКонтролируемыеСделкиСделки.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ПрочиеКонтролируемыеСделкиСделки.Количество КАК Количество,
	|	ПрочиеКонтролируемыеСделкиСделки.СуммаБезНДСВРублях КАК СуммаБезНДСВРублях,
	|	ВЫБОР
	|		КОГДА ПрочиеКонтролируемыеСделки.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА ПрочиеКонтролируемыеСделкиСделки.СуммаБезНДСВРублях
	|		ИНАЧЕ ПрочиеКонтролируемыеСделкиСделки.СуммаБезНДСВВалютеРасчетов
	|	КОНЕЦ КАК СуммаБезНДСВВалютеРасчетов,
	|	ПрочиеКонтролируемыеСделкиСделки.СуммаНДСВРублях КАК СуммаНДСВРублях,
	|	ВЫБОР
	|		КОГДА ПрочиеКонтролируемыеСделки.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА ПрочиеКонтролируемыеСделкиСделки.СуммаНДСВРублях
	|		ИНАЧЕ ПрочиеКонтролируемыеСделкиСделки.СуммаНДСВВалютеРасчетов
	|	КОНЕЦ КАК СуммаНДСВВалютеРасчетов,
	|	ПрочиеКонтролируемыеСделкиСделки.НаименованиеПредметаСделки КАК НаименованиеПредметаСделки,
	|	ПрочиеКонтролируемыеСделкиСделки.ТипПредметаСделки КАК ТипПредметаСделки,
	|	ПрочиеКонтролируемыеСделкиСделки.СтранаПроисхожденияПредметаСделки КАК СтранаПроисхожденияПредметаСделки,
	|	ПрочиеКонтролируемыеСделкиСделки.СтавкаНДС КАК СтавкаНДС,
	|	ПрочиеКонтролируемыеСделкиСделки.ТипКонтролируемойСделки КАК ТипКонтролируемойСделки,
	|	ПрочиеКонтролируемыеСделкиСделки.Грузоотправитель КАК Грузоотправитель,
	|	ПрочиеКонтролируемыеСделкиСделки.Грузополучатель КАК Грузополучатель,
	|	ПрочиеКонтролируемыеСделкиСделки.ОперацияОблагаетсяЕНВД КАК ОперацияОблагаетсяЕНВД,
	|	ВЫБОР
	|		КОГДА ПрочиеКонтролируемыеСделкиСделки.ТипПредметаСделки = ЗНАЧЕНИЕ(Перечисление.ТипыПредметовКонтролируемыхСделок.ДолговоеОбязательство)
	|			ТОГДА ПрочиеКонтролируемыеСделкиСделки.ПроцентнаяСтавка
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПроцентнаяСтавка,
	|	ВЫБОР
	|		КОГДА ПрочиеКонтролируемыеСделкиСделки.ТипПредметаСделки = ЗНАЧЕНИЕ(Перечисление.ТипыПредметовКонтролируемыхСделок.ДолговоеОбязательство)
	|			ТОГДА ПрочиеКонтролируемыеСделкиСделки.ДатаПроцентнойСтавки
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ДатаПроцентнойСтавки,
	|	ПрочиеКонтролируемыеСделки.Комиссионер КАК Комиссионер
	|ИЗ
	|	Документ.ПрочиеКонтролируемыеСделки.Сделки КАК ПрочиеКонтролируемыеСделкиСделки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрочиеКонтролируемыеСделки КАК ПрочиеКонтролируемыеСделки
	|		ПО ПрочиеКонтролируемыеСделкиСделки.Ссылка = ПрочиеКонтролируемыеСделки.Ссылка
	|ГДЕ
	|	ПрочиеКонтролируемыеСделки.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	
КонецПроцедуры

#КонецЕсли
