#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения, Истина);
	
	Если НЕ ЗначениеЗаполнено(СобытиеОС) Тогда
		СобытиеОС = УчетОС.ПолучитьСобытиеПоОСИзСправочника(Перечисления.ВидыСобытийОС.КапитальныйРемонт);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ)

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА

	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;
	Если СозданПриВводеНачальныхОстатков Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПроведения = Документы.ЗавершениеРемонтаОС.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ИНФОРМАЦИОННОЙ БАЗЫ
	УчетОС.ПроверитьСоответствиеОСОрганизации(ПараметрыПроведения.ОсновныеСредства,
		ПараметрыПроведения.Реквизиты, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Доначисляем амортизацию и списываем затраты по старым ремонтам ОС этого же вида
	РезультатыРасчета = Неопределено;
	УчетРемонтовОС.РассчитатьВыбытие(
		РезультатыРасчета, 
		ПараметрыПроведения.Реквизиты, 
		ПараметрыПроведения.ОсновныеСредства, 
		Отказ); 
	
	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ
	
	УчетРемонтовОС.ОтразитьВыбытиеВУчете(
		РезультатыРасчета, 
		ПараметрыПроведения.Реквизиты, 
		Движения, 
		Отказ);
	
	УчетРемонтовОС.ОтразитьОприходованиеМатериалов(
		ПараметрыПроведения.Материалы, 
		ПараметрыПроведения.Реквизиты, 
		Движения, 
		Отказ);
	
	Документы.ЗавершениеРемонтаОС.СформироватьДвиженияКапитализируемыеРемонтыОС(
		ПараметрыПроведения.ОсновныеСредства,
		ПараметрыПроведения.Реквизиты, 
		Движения, 
		Отказ);
	
	Документы.ЗавершениеРемонтаОС.СформироватьДвиженияСобытияОСОрганизаций(
		ПараметрыПроведения.ОсновныеСредства,
		ПараметрыПроведения.Реквизиты, 
		Движения, 
		Отказ);
	
	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Не СозданПриВводеНачальныхОстатков Тогда
		УправлениеВнеоборотнымиАктивами.ПроверитьОтсутствиеДублейВТабличнойЧасти(
			ЭтотОбъект, "ОС", Новый Структура("ОсновноеСредство"), Отказ);
	КонецЕсли;
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Если СозданПриВводеНачальныхОстатков Тогда
		НепроверяемыеРеквизиты.Добавить("ОС");
		НепроверяемыеРеквизиты.Добавить("ОС.ОсновноеСредство");
		НепроверяемыеРеквизиты.Добавить("ОС.СрокДоСледующегоРемонта");
	КонецЕсли;
	
	Если Не ОсталисьМатериалы Или СозданПриВводеНачальныхОстатков Тогда
		НепроверяемыеРеквизиты.Добавить("Склад");
		НепроверяемыеРеквизиты.Добавить("СчетУчетаДоходов");
		НепроверяемыеРеквизиты.Добавить("Материалы");
		НепроверяемыеРеквизиты.Добавить("Материалы.Номенклатура");
		НепроверяемыеРеквизиты.Добавить("Материалы.Количество");
		НепроверяемыеРеквизиты.Добавить("Материалы.СчетУчета");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
	Если ОсталисьМатериалы Тогда
		РеквизитыЗаСсылками = Документы.ЗавершениеРемонтаОС.РеквизитыЗаСсылками();
		СчетаУчетаВДокументах.ПроверитьЗаполнение(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, РеквизитыЗаСсылками);
		ПроверкаЗаполненияДокументов.ПроверитьРеквизитыЗаСсылками(
			ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, РеквизитыЗаСсылками);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли