#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	ВерсияДокумента = Документы.ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.АктуальнаяВерсия();
	ЗаполнитьДатуСсылку();
	ПрямыеВыплатыПособийСоциальногоСтрахования.ОбработкаЗаполненияЗаявленияОВыплатахРодителямДетейИнвалидов(
		ЭтотОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	МассивНепроверяемыхРеквизитов = Новый Массив;
	МассивНепроверяемыхРеквизитов.Добавить("Год");
	
	Если НЕ РегламентированнаяОтчетностьВызовСервера.ЭтоЮридическоеЛицо(Организация) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("КПП");
	КонецЕсли;
	
	Менеджер = Документы.ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов;
	ЭтоФормыПосле2022 = (Дата >= Менеджер.ДатаФорм2022());
	ЭтоФормыПосле2024 = (Дата >= Менеджер.ДатаФорм2024());
	ВидимостьПоляРебенок  = СЭДОФСС.ВидимостьПоляРодственник();
	ВидимостьПоляДокументОснование = Менеджер.ВидимостьОснования();
	
	МассивНепроверяемыхРеквизитов.Добавить("Оплаты.СотрудникСНИЛС");
	МассивНепроверяемыхРеквизитов.Добавить("Оплаты.Ребенок");
	МассивНепроверяемыхРеквизитов.Добавить("Оплаты.РебенокСНИЛС");
	МассивНепроверяемыхРеквизитов.Добавить("Оплаты.РебенокФамилия");
	МассивНепроверяемыхРеквизитов.Добавить("Оплаты.РебенокИмя");
	МассивНепроверяемыхРеквизитов.Добавить("Оплаты.РебенокДатаРождения");
	
	Для Каждого СтрокаТаблицы Из Оплаты Цикл
		ЕстьНакопленныеДни = ЗначениеЗаполнено(СтрокаТаблицы.НакопленоДнейЗаМесяц);
		ЕстьДниЗаГод       = ЗначениеЗаполнено(СтрокаТаблицы.КоличествоДнейЗаГод);
		ЕстьДниЗаМесяц     = ЗначениеЗаполнено(СтрокаТаблицы.КоличествоДней) Или (Не ЕстьДниЗаГод И Не ЕстьНакопленныеДни);
		Если ЭтоФормыПосле2022 Тогда
			Если Не ЗначениеЗаполнено(СтрокаТаблицы.Ребенок) И ВидимостьПоляРебенок Тогда
				ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаСтрокиТаблицы(Отказ, ЭтотОбъект, "Оплаты", СтрокаТаблицы, "Ребенок");
			Иначе
				ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаСтрокиТаблицы(Отказ, ЭтотОбъект, "Оплаты", СтрокаТаблицы, "РебенокФамилия");
				ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаСтрокиТаблицы(Отказ, ЭтотОбъект, "Оплаты", СтрокаТаблицы, "РебенокИмя");
				ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаСтрокиТаблицы(Отказ, ЭтотОбъект, "Оплаты", СтрокаТаблицы, "РебенокДатаРождения");
			КонецЕсли;
		КонецЕсли;
		Если ЭтоФормыПосле2024 И (ЕстьДниЗаГод Или ЕстьДниЗаМесяц) Тогда
			ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаСтрокиТаблицы(Отказ, ЭтотОбъект, "Оплаты", СтрокаТаблицы, "ПриказНомер");
			ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаСтрокиТаблицы(Отказ, ЭтотОбъект, "Оплаты", СтрокаТаблицы, "ПриказДата");
		КонецЕсли;
		Если Не ЭтоФормыПосле2022 Или ЕстьДниЗаМесяц Тогда
			ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаСтрокиТаблицы(Отказ, ЭтотОбъект, "Оплаты", СтрокаТаблицы, "СреднедневнойЗаработок");
		КонецЕсли;
		Если ЭтоФормыПосле2024 И ЕстьДниЗаГод Тогда
			ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаСтрокиТаблицы(Отказ, ЭтотОбъект, "Оплаты", СтрокаТаблицы, "СреднедневнойЗаработокЗаГод");
		КонецЕсли;
		Если Не ЭтоФормыПосле2024 Или ЕстьДниЗаМесяц Или ЕстьДниЗаГод Тогда
			ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаСтрокиТаблицы(Отказ, ЭтотОбъект, "Оплаты", СтрокаТаблицы, "КоличествоСтраниц");
			ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаСтрокиТаблицы(Отказ, ЭтотОбъект, "Оплаты", СтрокаТаблицы, "СуммаПособия");
		КонецЕсли;
	КонецЦикла;

	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьДатуСсылку();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьДатуСсылку()
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Год)
		И Дата >= Документы.ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.ДатаФорм2024() Тогда
		Год = Год(Дата);
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Ссылка)
		И Не ЗначениеЗаполнено(ПолучитьСсылкуНового())
		И ДополнительныеСвойства.Свойство("СсылкаНового") Тогда
		УстановитьСсылкуНового(ДополнительныеСвойства.СсылкаНового);
	КонецЕсли;
КонецПроцедуры

Функция ОбъектЗафиксирован() Экспорт
	Возврат НЕ ПрямыеВыплатыПособийСоциальногоСтрахования.СтатусПозволяетРедактироватьДокумент(СтатусДокумента);
КонецФункции

Функция ОбновитьВторичныеДанные(ПараметрыФиксации = Неопределено) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Модифицирован = Ложь;
	
	Если ОбъектЗафиксирован() Тогда
		Возврат Модифицирован;
	КонецЕсли;
	
	ЗаполнитьДатуСсылку();
	
	Если ПараметрыФиксации = Неопределено Тогда
		ПараметрыФиксации = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(Метаданные().ПолноеИмя()).ПараметрыФиксацииВторичныхДанных();
	КонецЕсли;
	
	Если ОбновитьДанныеОрганизации(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ОбновитьСведенияОРуководителе(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ОбновитьДанныеЛицевогоСчета(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ОбновитьДанныеРасширенная(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ОбновитьСведенияОСотрудниках(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ОбновитьСведенияОРодственниках(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ОбновитьДанныеРебенка(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Возврат Модифицирован;
КонецФункции

Функция ОбновитьДанныеОрганизации(ПараметрыФиксации)
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Реквизиты = Новый Структура(
	"РегистрационныйНомерФСС,
	|ДополнительныйКодФСС,
	|КодПодчиненностиФСС,
	|НаименованиеТерриториальногоОрганаФСС,
	|СтраховательНаименование,
	|ИНН,
	|КПП,
	|Руководитель,
	|ДолжностьРуководителя,
	|ОснованиеПодписиРуководителя,
	|ТелефонСоставителя,
	|АдресОрганизации,
	|АдресЭлектроннойПочтыОрганизации,
	|НаименованиеБанка,
	|НомерЛицевогоСчета,
	|НомерСчета,
	|БИКБанка");
	
	Если ЗначениеЗаполнено(Организация) Тогда
		СведенияОПодписях = ПодписиДокументов.СведенияОПодписяхПоУмолчаниюДляОбъектаМетаданных(Метаданные(), Организация);
		ЗаполнитьЗначенияСвойств(Реквизиты, СведенияОПодписях);
		
		ИменаПолей =
		"РегистрационныйНомерФСС,
		|КодПодчиненностиФСС,
		|ДополнительныйКодФСС,
		|НаименованиеТерриториальногоОрганаФСС,
		|НаимЮЛПол,
		|НаимЮЛСокр,
		|ИННЮЛ,
		|КППЮЛ,
		|БанкСчетНомер,
		|БанкСчетНаимБанка,
		|БанкСчетБИКБанка,
		|АдрЮР_JSON,
		|ТелОрганизации_JSON,
		|АдресЭлектроннойПочтыОрганизации";
		Сведения = СЭДОФСС.СведенияОСтрахователе(Организация, ИменаПолей, Дата);
		
		ЗаполнитьЗначенияСвойств(Реквизиты, Сведения);
		
		Реквизиты.ИНН                              = Сведения.ИННЮЛ;
		Реквизиты.КПП                              = Сведения.КППЮЛ;
		Реквизиты.НомерСчета                       = Сведения.БанкСчетНомер;
		Реквизиты.НаименованиеБанка                = Сведения.БанкСчетНаимБанка;
		Реквизиты.БИКБанка                         = Сведения.БанкСчетБИКБанка;
		Реквизиты.АдресОрганизации                 = Сведения.АдрЮР_JSON;
		Реквизиты.ТелефонСоставителя               = Сведения.ТелОрганизации_JSON;
		Реквизиты.АдресЭлектроннойПочтыОрганизации = Сведения.АдресЭлектроннойПочтыОрганизации;
		
		Если СтрДлина(Сведения.НаимЮЛПол) <= 150 Или Не ЗначениеЗаполнено(Сведения.НаимЮЛСокр) Тогда
			Реквизиты.СтраховательНаименование = Сведения.НаимЮЛПол;
		Иначе
			Реквизиты.СтраховательНаименование = Сведения.НаимЮЛСокр;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ОбновитьДанныеРасширенная(ПараметрыФиксации)
	Возврат ПрямыеВыплатыПособийСоциальногоСтрахования.ОбновитьДанныеЗаявленияОВыплатахРодителямДетейИнвалидов(
		ЭтотОбъект,
		ПараметрыФиксации);
КонецФункции

Функция ОбновитьСведенияОРуководителе(ПараметрыФиксации)
	Если Не ЗначениеЗаполнено(Руководитель) Тогда
		Возврат Ложь
	КонецЕсли;
	Реквизиты = Новый Структура("РуководительФИО");
	КадровыеДанныеФизическихЛиц = КадровыйУчет.КадровыеДанныеФизическихЛиц(
		Ложь,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Руководитель),
		"ФИОПолные",
		Дата);
	КадровыеДанные = КадровыеДанныеФизическихЛиц.Найти(Руководитель, "ФизическоеЛицо");
	Если КадровыеДанные <> Неопределено Тогда
		Реквизиты.РуководительФИО = КадровыеДанные.ФИОПолные;
	КонецЕсли;
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ОбновитьСведенияОСотрудниках(ПараметрыФиксации)
	Если Оплаты.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	МассивСотрудников = КоллекцииБЗК.УникальныеЗначенияКолонки(Оплаты, "Сотрудник");
	КоллекцииБЗК.УдалитьПустыеЗначенияМассива(МассивСотрудников);
	Если МассивСотрудников.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЕстьИзменения = Ложь;
	
	ИменаПолей =
	"Фамилия,
	|Имя,
	|Отчество,
	|СтраховойНомерПФР";
	КадровыеДанныеСотрудников = КадровыйУчет.КадровыеДанныеСотрудников(Ложь, МассивСотрудников, ИменаПолей, Дата);
	
	Реквизиты = Новый Структура("СотрудникФамилия, СотрудникИмя, СотрудникОтчество, СотрудникСНИЛС");
	
	Для Каждого СтрокаТаблицы Из Оплаты Цикл
		КадровыеДанные = КадровыеДанныеСотрудников.Найти(СтрокаТаблицы.Сотрудник, "Сотрудник");
		Если КадровыеДанные = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Реквизиты.СотрудникФамилия  = КадровыеДанные.Фамилия;
		Реквизиты.СотрудникИмя      = КадровыеДанные.Имя;
		Реквизиты.СотрудникОтчество = КадровыеДанные.Отчество;
		Реквизиты.СотрудникСНИЛС    = КадровыеДанные.СтраховойНомерПФР;
		Если ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеСтроки(
				Реквизиты,
				ЭтотОбъект,
				"Оплаты",
				СтрокаТаблицы,
				ПараметрыФиксации) Тогда
			ЕстьИзменения = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЕстьИзменения;
КонецФункции

Функция ОбновитьСведенияОРодственниках(ПараметрыФиксации)
	Если Не СЭДОФСС.ВидимостьПоляРодственник() Тогда
		Возврат Ложь;
	КонецЕсли;
	Если Оплаты.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЕстьИзменения = Ложь;
	
	МассивСсылок = КоллекцииБЗК.УникальныеЗначенияКолонки(Оплаты, "Ребенок");
	КоллекцииБЗК.УдалитьПустыеЗначенияМассива(МассивСсылок);
	Если МассивСсылок.Количество() > 0 Тогда
		ИменаПолей =
		"Фамилия,
		|Имя,
		|Отчество,
		|ДатаРождения,
		|СНИЛС,
		|СтепеньРодства,
		|КодСвязи";
		СведенияОРодственниках = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивСсылок, ИменаПолей, Ложь);
	Иначе
		СведенияОРодственниках = Новый Соответствие;
	КонецЕсли;
	
	ИменаРеквизитов = "РебенокФамилия, РебенокИмя, РебенокОтчество, РебенокДатаРождения, РебенокСНИЛС, Статус";
	
	Для Каждого СтрокаТаблицы Из Оплаты Цикл
		СведенияОРодственнике = СведенияОРодственниках[СтрокаТаблицы.Ребенок];
		Реквизиты = Новый Структура(ИменаРеквизитов);
		Если СведенияОРодственнике <> Неопределено Тогда
			Реквизиты.РебенокФамилия      = СведенияОРодственнике.Фамилия;
			Реквизиты.РебенокИмя          = СведенияОРодственнике.Имя;
			Реквизиты.РебенокОтчество     = СведенияОРодственнике.Отчество;
			Реквизиты.РебенокДатаРождения = СведенияОРодственнике.ДатаРождения;
			Реквизиты.РебенокСНИЛС        = СведенияОРодственнике.СНИЛС;
			// Список выбора поля "КодСвязи" см. в 
			// ПрямыеВыплатыПособийСоциальногоСтрахованияФормы.ЗаполнитьСписокВыбораТипРодственнойСвязи.
			Если СведенияОРодственнике.КодСвязи = "38" Тогда // 38 - мать.
				Реквизиты.Статус = "1";
			ИначеЕсли СведенияОРодственнике.КодСвязи = "39" Тогда // 39 - отец.
				Реквизиты.Статус = "2";
			ИначеЕсли СведенияОРодственнике.КодСвязи = "40" Тогда // 40 - опекун.
				Реквизиты.Статус = "3";
			ИначеЕсли СведенияОРодственнике.КодСвязи = "41" Тогда // 41 - попечитель.
				Реквизиты.Статус = "4";
			КонецЕсли;
		КонецЕсли;
		Если ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеСтроки(
				Реквизиты,
				ЭтотОбъект,
				"Оплаты",
				СтрокаТаблицы,
				ПараметрыФиксации) Тогда
			ЕстьИзменения = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЕстьИзменения;
КонецФункции

Функция ОбновитьДанныеЛицевогоСчета(ПараметрыФиксации)
	Сведения = ПредшествующиеСведенияОЛицевомСчете();
	Если Сведения = Неопределено Или Не ЗначениеЗаполнено(Сведения.КБК) Тогда
		Возврат Ложь;
	КонецЕсли;
	Реквизиты = Новый Структура("НомерЛицевогоСчета, КБК");
	ЗаполнитьЗначенияСвойств(Реквизиты, Сведения);
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ПредшествующиеСведенияОЛицевомСчете()
	Если Не ЗначениеЗаполнено(Организация) Или Не ЗначениеЗаполнено(НомерСчета) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Дата) Тогда
		НачалоДня = НачалоДня(Дата);
	Иначе
		НачалоДня = НачалоДня(ТекущаяДатаСеанса());
	КонецЕсли;
	КонецДня = КонецДня(НачалоДня) + 1;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Шапка.Ссылка КАК Ссылка,
	|	Шапка.НомерСчета КАК НомерСчета,
	|	Шапка.НомерЛицевогоСчета КАК НомерЛицевогоСчета,
	|	Шапка.КБК КАК КБК
	|ИЗ
	|	Документ.ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов КАК Шапка
	|ГДЕ
	|	Шапка.Организация = &Организация
	|	И Шапка.Дата < &КонецДня
	|	И Шапка.НомерСчета = &НомерСчета
	|	И Шапка.Проведен
	|	И ВЫБОР
	|			КОГДА Шапка.Дата >= &НачалоДня
	|				ТОГДА Шапка.Ссылка < &Ссылка
	|			ИНАЧЕ Шапка.Ссылка <> &Ссылка
	|		КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Шапка.Дата УБЫВ,
	|	Шапка.Ссылка";
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоДня", НачалоДня);
	Запрос.УстановитьПараметр("КонецДня", КонецДня);
	Запрос.УстановитьПараметр("НомерСчета", НомерСчета);
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Иначе
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст,
			"И ВЫБОР
			|			КОГДА Шапка.Дата >= &НачалоДня
			|				ТОГДА Шапка.Ссылка < &Ссылка
			|			ИНАЧЕ Шапка.Ссылка <> &Ссылка
			|		КОНЕЦ",
			"");
	КонецЕсли;
	
	Таблица = Запрос.Выполнить().Выгрузить();
	Если Таблица.Количество() > 0 Тогда
		Возврат Таблица[0];
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

Функция ОбновитьДанныеРебенка(ПараметрыФиксации)
	Если ВерсияДокумента < 1 Тогда
		Возврат Ложь;
	КонецЕсли;
	Если Оплаты.Количество() < 2 Тогда
		Возврат Ложь;
	КонецЕсли;
	ЕстьИзменения = Ложь;
	
	ИменаПолейДанныхРебенка = "РебенокФамилия, РебенокИмя, РебенокОтчество, РебенокДатаРождения, РебенокСНИЛС";
	ИменаПолейДанныхСправки = "СправкаНомер, СправкаДатаВыдачи, СправкаДатаОкончания, СправкаДатаУстановления";
	
	ДанныеСправокПоСНИЛС = ДанныеСправокПоСНИЛС(ИменаПолейДанныхСправки);
	
	СтрокаЗаполненияДанныхРебенка = Неопределено;
	СтрокаЗаполненияДанныхСправки = Неопределено;
	
	ПустыеЗначенияДанныхРебенка = Новый Структура(ИменаПолейДанныхРебенка);
	ПустыеЗначенияДанныхСправки = Новый Структура(ИменаПолейДанныхСправки);
	
	НовыеЗначенияДанныхРебенка  = Новый Структура(ИменаПолейДанныхРебенка);
	НовыеЗначенияДанныхСправки  = Новый Структура(ИменаПолейДанныхСправки);
	
	Для Каждого СтрокаТаблицы Из Оплаты Цикл
		// Заполнение ФИО, даты рождения и СНИЛС.
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.Ребенок) Тогда
			Если СтрокаЗаполненияДанныхРебенка <> Неопределено
				И СтрокаЗаполненияДанныхРебенка.Сотрудник = СтрокаТаблицы.Сотрудник
				И СтрокаЗаполненияДанныхРебенка.Ребенок = СтрокаТаблицы.Ребенок Тогда
				Значения = НовыеЗначенияДанныхРебенка;
			Иначе
				Значения = ПустыеЗначенияДанныхРебенка;
			КонецЕсли;
			Если ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеСтроки(
					Значения,
					ЭтотОбъект,
					"Оплаты",
					СтрокаТаблицы,
					ПараметрыФиксации) Тогда
				ЕстьИзменения = Истина;
			КонецЕсли;
		КонецЕсли;
		
		// Заполнение данных справки.
		ДанныеСправкиПоСНИЛС = ДанныеСправокПоСНИЛС[СтрокаТаблицы.РебенокСНИЛС];
		Если ДанныеСправкиПоСНИЛС <> Неопределено Тогда
			Значения = ДанныеСправкиПоСНИЛС;
		ИначеЕсли СтрокаЗаполненияДанныхСправки <> Неопределено
			И СтрокаЗаполненияДанныхСправки.Сотрудник = СтрокаТаблицы.Сотрудник
			И СтрокаЗаполненияДанныхСправки.Ребенок = СтрокаТаблицы.Ребенок Тогда
			Значения = НовыеЗначенияДанныхСправки;
		Иначе
			Значения = ПустыеЗначенияДанныхСправки;
		КонецЕсли;
		Если ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеСтроки(
				Значения,
				ЭтотОбъект,
				"Оплаты",
				СтрокаТаблицы,
				ПараметрыФиксации) Тогда
			ЕстьИзменения = Истина;
		КонецЕсли;
		
		// Определение наличия ручных изменений для заполнения последующих строк.
		Фильтр = Новый Структура("Путь, ИдентификаторСтроки", "Оплаты", СтрокаТаблицы.ИдентификаторСтрокиФикс);
		ЗафиксированныеРеквизиты = ФиксацияИзменений.Выгрузить(Фильтр, "ИмяРеквизита").ВыгрузитьКолонку("ИмяРеквизита");
		
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.Ребенок) Тогда
			Если (ЗначениеЗаполнено(СтрокаТаблицы.РебенокФамилия)
					И ЗафиксированныеРеквизиты.Найти("РебенокФамилия") <> Неопределено)
				Или (ЗначениеЗаполнено(СтрокаТаблицы.РебенокИмя)
					И ЗафиксированныеРеквизиты.Найти("РебенокИмя") <> Неопределено)
				Или (ЗначениеЗаполнено(СтрокаТаблицы.РебенокДатаРождения)
					И ЗафиксированныеРеквизиты.Найти("РебенокДатаРождения") <> Неопределено)
				Или (ЗначениеЗаполнено(СтрокаТаблицы.РебенокСНИЛС)
					И ЗафиксированныеРеквизиты.Найти("РебенокСНИЛС") <> Неопределено) Тогда
				СтрокаЗаполненияДанныхРебенка = СтрокаТаблицы;
				ЗаполнитьЗначенияСвойств(НовыеЗначенияДанныхРебенка, СтрокаТаблицы);
			КонецЕсли;
		КонецЕсли;
		Если (ЗначениеЗаполнено(СтрокаТаблицы.СправкаНомер)
				И ЗафиксированныеРеквизиты.Найти("СправкаНомер") <> Неопределено)
			Или (ЗначениеЗаполнено(СтрокаТаблицы.СправкаДатаВыдачи)
				И ЗафиксированныеРеквизиты.Найти("СправкаДатаВыдачи") <> Неопределено)
			Или (ЗначениеЗаполнено(СтрокаТаблицы.СправкаДатаОкончания)
				И ЗафиксированныеРеквизиты.Найти("СправкаДатаОкончания") <> Неопределено)
			Или (ЗначениеЗаполнено(СтрокаТаблицы.СправкаДатаУстановления)
				И ЗафиксированныеРеквизиты.Найти("СправкаДатаУстановления") <> Неопределено) Тогда
			СтрокаЗаполненияДанныхСправки = СтрокаТаблицы;
			ЗаполнитьЗначенияСвойств(НовыеЗначенияДанныхСправки, СтрокаЗаполненияДанныхСправки);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЕстьИзменения;
КонецФункции

Функция ДанныеСправокПоСНИЛС(ИменаПолейДанныхСправки)
	Результат = Новый Соответствие;
	СНИЛСы = КоллекцииБЗК.УникальныеЗначенияКолонки(Оплаты, "РебенокСНИЛС");
	КоллекцииБЗК.УдалитьПустыеЗначенияМассива(СНИЛСы);
	
	Если СНИЛСы.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата",   Дата);
	Запрос.УстановитьПараметр("СНИЛСы", СНИЛСы);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ФизическиеЛица.Ссылка КАК Ссылка,
	|	ФизическиеЛица.СтраховойНомерПФР КАК СтраховойНомерПФР
	|ПОМЕСТИТЬ Физлица
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ФизическиеЛица
	|ГДЕ
	|	ФизическиеЛица.СтраховойНомерПФР В(&СНИЛСы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Физлица.Ссылка КАК Ссылка,
	|	Физлица.СтраховойНомерПФР КАК СтраховойНомерПФР,
	|	СведенияОбИнвалидностиФизическихЛиц.Период КАК Период,
	|	СведенияОбИнвалидностиФизическихЛиц.ДействуетДо КАК ДействуетДо,
	|	СведенияОбИнвалидностиФизическихЛиц.ДатаВыдачи КАК ДатаВыдачи,
	|	СведенияОбИнвалидностиФизическихЛиц.СрокДействияСправки КАК СрокДействияСправки,
	|	СведенияОбИнвалидностиФизическихЛиц.СерияСправки КАК СерияСправки,
	|	СведенияОбИнвалидностиФизическихЛиц.НомерСправки КАК НомерСправки
	|ИЗ
	|	Физлица КАК Физлица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбИнвалидностиФизическихЛиц КАК СведенияОбИнвалидностиФизическихЛиц
	|		ПО Физлица.Ссылка = СведенияОбИнвалидностиФизическихЛиц.ФизическоеЛицо
	|			И (СведенияОбИнвалидностиФизическихЛиц.Период <= &Дата)
	|			И (СведенияОбИнвалидностиФизическихЛиц.Инвалидность)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";
	
	Таблица = Запрос.Выполнить().Выгрузить();
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		СправкаНомер = СокрЛП(СтрокаТаблицы.СерияСправки + " " + СтрокаТаблицы.НомерСправки);
		Если СправкаНомер = "" Тогда
			Продолжить;
		КонецЕсли;
		ДанныеСправки = Результат[СтрокаТаблицы.СтраховойНомерПФР];
		Если ДанныеСправки <> Неопределено Тогда
			Если ДанныеСправки.СправкаДатаВыдачи > СтрокаТаблицы.ДатаВыдачи Тогда
				Продолжить;
			КонецЕсли;
		Иначе
			ДанныеСправки = Новый Структура(ИменаПолейДанныхСправки);
			Результат.Вставить(СтрокаТаблицы.СтраховойНомерПФР, ДанныеСправки);
		КонецЕсли;
		ДанныеСправки.СправкаНомер            = СправкаНомер;
		ДанныеСправки.СправкаДатаВыдачи       = СтрокаТаблицы.ДатаВыдачи;
		ДанныеСправки.СправкаДатаОкончания    = СтрокаТаблицы.СрокДействияСправки;
		ДанныеСправки.СправкаДатаУстановления = СтрокаТаблицы.Период;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли