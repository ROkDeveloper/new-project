#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВремяДокументаПоУмолчанию() Экспорт
	
	Возврат Новый Структура("Часы, Минуты", 20, 0);
	
КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	ИЛИ ЗначениеРазрешено(ОрганизацияПолучатель)
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Проведение. Подготовка параметров проведения документа

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", 			ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация
	|ИЗ
	|	Документ.АвизоПрочееИсходящее КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	Результат    	= Запрос.Выполнить();

	Реквизиты       = ОбщегоНазначенияБПВызовСервера.ПолучитьСтруктуруИзРезультатаЗапроса(Результат);
	Период          = Реквизиты.Период;
	Организация     = Реквизиты.Организация;

	Если НЕ УчетнаяПолитика.Существует(Реквизиты.Организация, Реквизиты.Период, Истина, ДокументСсылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ПрименениеПБУ18", 		 УчетнаяПолитика.ПоддержкаПБУ18(Реквизиты.Организация, Реквизиты.Период));
	Запрос.УстановитьПараметр("ОтражатьВНалоговомУчете", УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Организация, Период));
	
	НомераТаблиц = Новый Структура;

	Запрос.Текст = ТекстЗапросаРеквизитыДокумента(НомераТаблиц)
		+ ТекстЗапросаДанныеБух(НомераТаблиц);

	Результат = Запрос.ВыполнитьПакет();

	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;

	Возврат ПараметрыПроведения;

КонецФункции

Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц = Неопределено)

	НомераТаблиц.Вставить("Реквизиты", НомераТаблиц.Количество());	
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ОрганизацияПолучатель КАК ОрганизацияПолучатель,
	|	Реквизиты.СчетРасчетов КАК СчетРасчетов,
	|	&ПрименениеПБУ18 КАК ПрименениеПБУ18,
	|	&ОтражатьВНалоговомУчете КАК ОтражатьВНалоговомУчете
	|ИЗ
	|	Документ.АвизоПрочееИсходящее КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаДанныеБух(НомераТаблиц)

	НомераТаблиц.Вставить("ДанныеБух", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеБух.Ссылка КАК Регистратор,
	|	ДанныеБух.НомерСтроки,
	|	ДанныеБух.СчетУчета,
	|	ДанныеБух.Подразделение,
	|	ДанныеБух.Субконто1,
	|	ДанныеБух.Субконто2,
	|	ДанныеБух.Субконто3,
	|	ДанныеБух.Сумма,
	|	ДанныеБух.СуммаНУ,
	|	ДанныеБух.СуммаПР,
	|	ДанныеБух.СуммаВР,
	|	ДанныеБух.СуммаКТ,
	|	ДанныеБух.Количество,
	|	ДанныеБух.Валюта,
	|	ДанныеБух.ВалютнаяСумма,
	|	ДанныеБух.СодержаниеПроводки
	|ИЗ
	|	Документ.АвизоПрочееИсходящее.ДанныеБух КАК ДанныеБух
	|ГДЕ
	|	ДанныеБух.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Проведение. Формирование движений

Функция ПодготовитьПараметрыДанныеБух(ТаблицаДанныеБух, ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы Параметры.ДанныеБух
	
	СписокОбязательныхКолонок = ""
	+ "СчетУчета,"           // <План счетов бухгалтерского учета>
	+ "Субконто1," 			 // <Характеристика.ВидыСубконтоХозрасчетные>
	+ "Субконто2," 			 // <Характеристика.ВидыСубконтоХозрасчетные>
	+ "Субконто3," 			 // <Характеристика.ВидыСубконтоХозрасчетные>
	+ "Подразделение,"       // <СправочникСсылка.Подразделения организаций>
	+ "Сумма,"           	 // <Число (15,2)>
	+ "СуммаКт,"         	 // <Число (15,2)>
	+ "Валюта,"              // <СправочникСсылка.Валюта>
	+ "ВалютнаяСумма,"       // <Число (15,3)>
	+ "Количество,"   		 // <Число (15,3)>
	+ "СуммаНУ,"             // <Число (15,2)>
	+ "СуммаПР,"             // <Число (15,2)>
	+ "СуммаВР,"             // <Число (15,2)>
	+ "СодержаниеПроводки,"; // <Строка,0>

	Параметры.Вставить("ТаблицаДанныеБух",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаДанныеБух, СписокОбязательныхКолонок));

	// Подготовка таблицы Параметры.Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Период,"                         // <Дата> - период движений - дата документа
	+ "Организация,"                    // <СправочникСсылка.Организации>
	+ "ОрганизацияПолучатель,"       	// <СправочникСсылка.Организации>
	+ "СчетРасчетов,"                   // <План счетов бухгалтерского учета>
	+ "ПрименениеПБУ18,"       			// <Булево>
	+ "ОтражатьВНалоговомУчете,";       // <Булево>

	Параметры.Вставить("Реквизиты",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

Процедура СформироватьДвиженияДанныеБух(ТаблицаДанныеБух, ТаблицаРеквизиты, Движения, Отказ) Экспорт

	Проводки 	= Движения.Хозрасчетный;
	
	Параметры 	= ПодготовитьПараметрыДанныеБух(ТаблицаДанныеБух, ТаблицаРеквизиты);
   	Реквизиты 	= Параметры.Реквизиты[0];
	
	Для Каждого СтрокаТЧ Из Параметры.ТаблицаДанныеБух Цикл
		
		ОтразитьПоДебету 	= (СтрокаТЧ.Сумма <> 0);
		СуммаПроводки 		= ?(ОтразитьПоДебету, СтрокаТЧ.Сумма, СтрокаТЧ.СуммаКт);
		СвойстваСчетаУчета 	= БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТЧ.СчетУчета);
		
		Проводка = Проводки.Добавить();
		Проводка.Период      = Реквизиты.Период;
		Проводка.Организация = Реквизиты.Организация;
		Проводка.Содержание  = СтрокаТЧ.СодержаниеПроводки;
		Проводка.Сумма 		 = СуммаПроводки;
		
		Если ОтразитьПоДебету Тогда
			
			Проводка.СчетДт = СтрокаТЧ.СчетУчета;
			Для Инд = 1 По 3 Цикл
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, Инд, СтрокаТЧ["Субконто" + Инд]);
			КонецЦикла;
			
			БухгалтерскийУчет.УстановитьПодразделениеПроводки(Проводка, СтрокаТЧ.Подразделение, "Дт");
			
			Если СвойстваСчетаУчета.Валютный Тогда
				Проводка.ВалютаДт 			= СтрокаТЧ.Валюта;
				Проводка.ВалютнаяСуммаДт 	= СтрокаТЧ.ВалютнаяСумма;
			КонецЕсли;
			Если СвойстваСчетаУчета.Количественный Тогда
				Проводка.КоличествоДт 		= СтрокаТЧ.Количество;
			КонецЕсли;
			
			Если НЕ СвойстваСчетаУчета.Забалансовый Тогда
				Проводка.СчетКт = Реквизиты.СчетРасчетов;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1, Реквизиты.ОрганизацияПолучатель);
			КонецЕсли;
			
		Иначе
			
			Если НЕ СвойстваСчетаУчета.Забалансовый Тогда
				Проводка.СчетДт = Реквизиты.СчетРасчетов;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, Реквизиты.ОрганизацияПолучатель);
			КонецЕсли;
			
			Проводка.СчетКт = СтрокаТЧ.СчетУчета;
			Для Инд = 1 По 3 Цикл
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, Инд, СтрокаТЧ["Субконто" + Инд]);
			КонецЦикла;
			
			БухгалтерскийУчет.УстановитьПодразделениеПроводки(Проводка, СтрокаТЧ.Подразделение, "Кт");
			
			Если СвойстваСчетаУчета.Валютный Тогда
				Проводка.ВалютаКт 			= СтрокаТЧ.Валюта;
				Проводка.ВалютнаяСуммаКт 	= СтрокаТЧ.ВалютнаяСумма;
			КонецЕсли;
			Если СвойстваСчетаУчета.Количественный Тогда
				Проводка.КоличествоКт 		= СтрокаТЧ.Количество;
			КонецЕсли;
			
		КонецЕсли;
		
		Если Реквизиты.ОтражатьВНалоговомУчете Тогда
			
			НалоговыйУчет.ЗаполнитьНалоговыеСуммыПроводки(СтрокаТЧ.СуммаНУ,СтрокаТЧ.СуммаНУ,
				СтрокаТЧ.СуммаПР,СтрокаТЧ.СуммаПР,
				СтрокаТЧ.СуммаВР,СтрокаТЧ.СуммаВР,
				Проводка, Реквизиты.ПрименениеПБУ18);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Проводки.Записывать = Истина;


КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Авизо
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Авизо";
	КомандаПечати.Представление = НСтр("ru = 'Авизо'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор  = "Реестр";
	КомандаПечати.Представление  = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы = НСтр("ru = 'Реестр документов ""Авизо по прочим операциям исходящее""'");
	КомандаПечати.Обработчик     = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм     = "ФормаСписка";
	КомандаПечати.Порядок        = 100;
	
КонецПроцедуры

Функция ПолучитьПериодВыборкиДвиженийССубконто(МассивОбъектов)

	СтруктураДат = Новый Структура("ДатаОТ, ДатаДО");
	
	ЗапросПериодОборотов = Новый Запрос;
	ЗапросПериодОборотов.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	ЗапросПериодОборотов.Текст = 
	"ВЫБРАТЬ
	|	МИНИМУМ(Док.Дата) КАК ДатаОТ,
	|	МАКСИМУМ(Док.Дата) КАК ДатаДО
	|ИЗ
	|	Документ.АвизоПрочееИсходящее КАК Док
	|ГДЕ
	|	Док.Ссылка В(&МассивОбъектов)";
	
	ВыборкаПериодОборотов = ЗапросПериодОборотов.Выполнить().Выбрать();
	ВыборкаПериодОборотов.Следующий();
	
	СтруктураДат.Вставить("ДатаОТ", 	ВыборкаПериодОборотов.ДатаОТ - 1);
	СтруктураДат.Вставить("ДатаДО", 	ВыборкаПериодОборотов.ДатаДО + 1);

	Возврат СтруктураДат;
	
КонецФункции

// Формирует и возвращает текст запроса для выборки данных,
// необходимых для формирования печатной формы
Функция ПолучитьТекстЗапросаДляФормированияПечатнойФормыАвизо()
	
	ТекстЗапроса =   
	"ВЫБРАТЬ
	|	Авизо.Ссылка,
	|	Авизо.Номер,
	|	Авизо.Дата,
	|	Авизо.СчетРасчетов,
	|	Авизо.Организация КАК ОрганизацияОтправитель,
	|	ВЫБОР
	|		КОГДА Авизо.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА Авизо.Организация
	|		ИНАЧЕ Авизо.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК Организация,
	|	Авизо.ОрганизацияПолучатель КАК ОрганизацияПолучатель,
	|	Авизо.Ответственный,
	|	Авизо.Ответственный.Представление КАК ОтветственныйПредставление
	|ПОМЕСТИТЬ ТаблицаШапка
	|ИЗ
	|	Документ.АвизоПрочееИсходящее КАК Авизо
	|ГДЕ
	|	Авизо.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаШапка.Ссылка КАК Ссылка,
	|	ТаблицаШапка.Номер КАК Номер,
	|	ТаблицаШапка.Дата КАК Дата,
	|	ТаблицаШапка.СчетРасчетов,
	|	ТаблицаШапка.ОрганизацияОтправитель,
	|	ТаблицаШапка.Организация,
	|	ТаблицаШапка.ОрганизацияПолучатель,
	|	ТаблицаШапка.Ответственный,
	|	ТаблицаШапка.ОтветственныйПредставление
	|ИЗ
	|	ТаблицаШапка КАК ТаблицаШапка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйДвиженияССубконто.Регистратор КАК Регистратор,
	|	ХозрасчетныйДвиженияССубконто.НомерСтроки КАК НомерСтроки,
	|	ХозрасчетныйДвиженияССубконто.СчетДт,
	|	ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	|	ХозрасчетныйДвиженияССубконто.СубконтоДт2,
	|	ХозрасчетныйДвиженияССубконто.СубконтоДт3,
	|	ХозрасчетныйДвиженияССубконто.СчетКт,
	|	ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	|	ХозрасчетныйДвиженияССубконто.СубконтоКт2,
	|	ХозрасчетныйДвиженияССубконто.СубконтоКт3,
	|	ХозрасчетныйДвиженияССубконто.Организация,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйДвиженияССубконто.ВалютаДт ЕСТЬ NULL 
	|			ТОГДА ХозрасчетныйДвиженияССубконто.ВалютаКт
	|		ИНАЧЕ ХозрасчетныйДвиженияССубконто.ВалютаДт
	|	КОНЕЦ КАК Валюта,
	|	ХозрасчетныйДвиженияССубконто.Сумма,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйДвиженияССубконто.ВалютнаяСуммаДт ЕСТЬ NULL 
	|			ТОГДА ХозрасчетныйДвиженияССубконто.ВалютнаяСуммаКт
	|		ИНАЧЕ ХозрасчетныйДвиженияССубконто.ВалютнаяСуммаДт
	|	КОНЕЦ КАК ВалСумма,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйДвиженияССубконто.КоличествоДт ЕСТЬ NULL 
	|			ТОГДА ХозрасчетныйДвиженияССубконто.КоличествоКт
	|		ИНАЧЕ ХозрасчетныйДвиженияССубконто.КоличествоДт
	|	КОНЕЦ КАК Количество,
	|	ХозрасчетныйДвиженияССубконто.Содержание,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйДвиженияССубконто.СуммаНУДт ЕСТЬ NULL 
	|			ТОГДА ХозрасчетныйДвиженияССубконто.СуммаНУКт
	|		ИНАЧЕ ХозрасчетныйДвиженияССубконто.СуммаНУДт
	|	КОНЕЦ КАК СуммаНУ,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйДвиженияССубконто.СуммаПРДт ЕСТЬ NULL 
	|			ТОГДА ХозрасчетныйДвиженияССубконто.СуммаПРКт
	|		ИНАЧЕ ХозрасчетныйДвиженияССубконто.СуммаПРДт
	|	КОНЕЦ КАК СуммаПР,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйДвиженияССубконто.СуммаВРДт ЕСТЬ NULL 
	|			ТОГДА ХозрасчетныйДвиженияССубконто.СуммаВРКт
	|		ИНАЧЕ ХозрасчетныйДвиженияССубконто.СуммаВРДт
	|	КОНЕЦ КАК СуммаВР
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|			&ДатаОТ,
	|			&ДатаДО,
	|			(Регистратор, СчетДт) В
	|					(ВЫБРАТЬ
	|						ТаблицаШапка.Ссылка,
	|						ТаблицаШапка.СчетРасчетов
	|					ИЗ
	|						ТаблицаШапка)
	|				ИЛИ (Регистратор, СчетКт) В
	|					(ВЫБРАТЬ
	|						ТаблицаШапка.Ссылка,
	|						ТаблицаШапка.СчетРасчетов
	|					ИЗ
	|						ТаблицаШапка),
	|			,
	|			) КАК ХозрасчетныйДвиженияССубконто
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор,
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция формирует табличный документ с печатной формой накладной,
// разработанной методистами
//
// Возвращаемое значение:
//  Табличный документ - печатная форма накладной
//
Функция ПечатьАвизо(МассивОбъектов, ОбъектыПечати)

	УстановитьПривилегированныйРежим(Истина);
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.АвтоМасштаб			= Истина;
	ТабДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_АвизоПрочееИсходящее_Авизо";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	СтруктураДат = ПолучитьПериодВыборкиДвиженийССубконто(МассивОбъектов);
	Запрос.УстановитьПараметр("ДатаОТ", 		СтруктураДат.ДатаОТ);
	Запрос.УстановитьПараметр("ДатаДО", 		СтруктураДат.ДатаДО);
	
	Запрос.Текст = ПолучитьТекстЗапросаДляФормированияПечатнойФормыАвизо();
	
	Результаты = Запрос.ВыполнитьПакет();
	
	Шапка 				= Результаты[1].Выбрать();
	ВыборкаДвижений 	= Результаты[2].Выбрать();

	ПервыйДокумент = Истина;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АвизоПрочееИсходящее.ПФ_MXL_Авизо");
	
	Пока Шапка.СледующийПоЗначениюПоля("Ссылка") Цикл
		
		Если НЕ ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
		
		// Выводим основную шапку
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакета.Параметры.ТекстЗаголовка = ОбщегоНазначенияБПВызовСервера.СформироватьЗаголовокДокумента(Шапка, НСтр("ru = 'Извещение (авизо)'"));
		
		СведенияОбОрганизации    = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Шапка.Организация, Шапка.Дата);
		ПредставлениеОрганизации = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм,");
		ОбластьМакета.Параметры.Организация = ПредставлениеОрганизации;
		
		СведенияОбОтправителе    = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Шапка.ОрганизацияОтправитель, Шапка.Дата);
		ПредставлениеОтправителя = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОтправителе, "НаименованиеДляПечатныхФорм,");
		ОбластьМакета.Параметры.Отправитель = ПредставлениеОтправителя;
		
		СведенияОПолучателе     = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Шапка.ОрганизацияПолучатель, Шапка.Дата);
		ПредставлениеПолучателя = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОПолучателе, "НаименованиеДляПечатныхФорм,");
		ОбластьМакета.Параметры.Получатель = ПредставлениеПолучателя;
		
		ТабДокумент.Вывести(ОбластьМакета);
		
		// Выводим шапку таблицы БУ
		ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокТаблицыБУ");
		ТабДокумент.Вывести(ОбластьМакета);
		
		ВыборкаДвижений.Сбросить();
		СтруктураПоиска = Новый Структура("Регистратор", Шапка.Ссылка);
		
		НС = 1;
		// Выводим строки таблицы БУ
		Пока ВыборкаДвижений.НайтиСледующий(СтруктураПоиска) Цикл
			
			ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТаблицыБУ");
			ОбластьМакета.Параметры.Заполнить(ВыборкаДвижений);
			ОбластьМакета.Параметры.НомерСтроки = НС;
			
			АналитикаДт = 
				?(ЗначениеЗаполнено(ВыборкаДвижений.СубконтоДт1), Строка(ВыборкаДвижений.СубконтоДт1) + Символы.ПС, "")
				+ ?(ЗначениеЗаполнено(ВыборкаДвижений.СубконтоДт2), Строка(ВыборкаДвижений.СубконтоДт2) + Символы.ПС, "")
				+ ?(ЗначениеЗаполнено(ВыборкаДвижений.СубконтоДт3), Строка(ВыборкаДвижений.СубконтоДт3), "");
			
			АналитикаКт = 
				?(ЗначениеЗаполнено(ВыборкаДвижений.СубконтоКт1), Строка(ВыборкаДвижений.СубконтоКт1) + Символы.ПС, "")
				+ ?(ЗначениеЗаполнено(ВыборкаДвижений.СубконтоКт2), Строка(ВыборкаДвижений.СубконтоКт2) + Символы.ПС, "")
				+ ?(ЗначениеЗаполнено(ВыборкаДвижений.СубконтоКт3), Строка(ВыборкаДвижений.СубконтоКт3), "");
			
			ОбластьМакета.Параметры.АналитикаДт = АналитикаДт;
			ОбластьМакета.Параметры.АналитикаКт = АналитикаКт;
			
			ТабДокумент.Вывести(ОбластьМакета);
			НС = НС +1;		
		КонецЦикла;
		
		// Выводим подвал таблицы БУ
		ОбластьМакета = Макет.ПолучитьОбласть("ПодвалТаблицыБУ");
		ТабДокумент.Вывести(ОбластьМакета);
		
		// Выводим общий подвал
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
		ОбластьМакета.Параметры.РасшифровкаПодписиИсполнителя = ?(НЕ ЗначениеЗаполнено(Шапка.ОтветственныйПредставление), "", 
																					Шапка.ОтветственныйПредставление);
		ТабДокумент.Вывести(ОбластьМакета);
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДокумент;

КонецФункции // ПечатьПеремещениеТоваров()

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Авизо") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Авизо", "Авизо", 
			ПечатьАвизо(МассивОбъектов, ОбъектыПечати), , "Документ.АвизоПрочееИсходящее.ПФ_MXL_Авизо");
	КонецЕсли;
	
	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);	
	
КонецПроцедуры

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура("Информация", "ОрганизацияПолучатель");
	
	Возврат Результат;
	
КонецФункции

#КонецЕсли