#Область СлужебныйПрограммныйИнтерфейс

Процедура НаписатьОбращениеВТехподдержку1СОшибкаАУСН(АдресДанных, Ключ) Экспорт
	
	ПараметрыОбращения =
		РаботаСОповещениямиОСостоянииОбменаССервисамиВызовСервера.ПараметрыОбращенияВТехподдержкуОшибкаАУСН(
			АдресДанных);
	
	Если ПараметрыОбращения.ЭтоРежимСервиса Тогда
		ПараметрыФормы = Новый Структура("СоздаватьСообщение", Истина);
		ФормаОбращения = ПолучитьФорму(
			"Обработка.ИнформационныйЦентр.Форма.ОтправкаСообщенияВСлужбуПоддержки",
			ПараметрыФормы,
			,
			Ключ);
		ФормаОбращения.Тема = ПараметрыОбращения.Тема;
		ФормаОбращения.Содержание.Удалить();
		ФормаОбращения.Содержание.Добавить(
			ПараметрыОбращения.ПорядокВоспроизведения, Тип("ТекстФорматированногоДокумента"));
		ФормаОбращения.Открыть();
	Иначе
		ДанныеСообщения = СообщенияВСлужбуТехническойПоддержкиКлиентСервер.ДанныеСообщения();
		ДанныеСообщения.Получатель = СообщенияВСлужбуТехническойПоддержкиБПКлиентСервер.ЛинияПоддержки();
		ДанныеСообщения.ИспользоватьСтандартныйШаблон = Ложь;
		ДанныеСообщения.ИНН = ПараметрыОбращения.ИННОрганизации;
		ДанныеСообщения.Тема = ПараметрыОбращения.Тема;
		ДанныеСообщения.Сообщение = ПараметрыОбращения.ПорядокВоспроизведения;
		СообщенияВСлужбуТехническойПоддержкиКлиент.ОтправитьСообщение(ДанныеСообщения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьИнформациюОбОшибкеАУСН(АдресДанных, Ключ) Экспорт
	
	ПараметрыФормы = РаботаСОповещениямиОСостоянииОбменаССервисамиВызовСервера.ПараметрыФормыИнформацииОбОшибкеАУСН(
		АдресДанных);
	ОткрытьФорму("ОбщаяФорма.ИнформацияОбОшибкеОбменаАУСН", ПараметрыФормы, , Ключ);
	
КонецПроцедуры

Процедура ОткрытьОшибкиОтправкиАУСН(АдресДанных, Ключ) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДанныеОбмена", АдресДанных);
	
	ОткрытьФорму("ОбщаяФорма.ОшибкиОтправкиАУСН", ПараметрыФормы, , Ключ);
	
КонецПроцедуры

#КонецОбласти