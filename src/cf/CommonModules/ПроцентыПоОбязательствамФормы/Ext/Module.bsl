// Содержимое модуля отличается в версиях ПРОФ и КОРП.
// В версии ПРОФ отсутствует:
// - тело экспортных процедур (остаются только сигнатуры методов)
// - неэкспортные процедуры и все функции.
// Поэтому не допускается реализация в модуле экспортных функций: вместо них следует использовать процедуры с возвращаемым параметром.

#Область СлужебныйПрограммныйИнтерфейс

Процедура УстановитьУсловноеОформление(Форма) Экспорт
	
	// НадписьГрафикПлатежей
	ЭлементУО = Форма.УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "НадписьГрафикПлатежей");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ГрафикПлатежейЗаполнен", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненныйРеквизит);
	
КонецПроцедуры

Процедура УстановитьУсловноеОформлениеВводОстатков(Форма) Экспорт
	
	// НадписьГрафикПроцентныхРасходов
	ЭлементУО = Форма.УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "НадписьГрафикПроцентныхРасходов");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ГрафикПроцентныхРасходовЗаполнен", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненныйРеквизит);
	
КонецПроцедуры

Процедура ЗаполнитьРассчитатьСуммы(Форма, ПараметрыСтарогоКурса, ПараметрыНовогоКурса, ТабЧасть) Экспорт
	
	Объект = Форма.Объект;
	
	СуммаПлатежаСтарая = Объект.ГрафикПлатежей.Итог("СуммаПлатежа");
	
	Если СуммаПлатежаСтарая > 0 Тогда
		
		ТаблицаГрафикПлатежей = Объект.ГрафикПлатежей.Выгрузить();
		
		СуммыПлатежейСтарые = ТаблицаГрафикПлатежей.ВыгрузитьКолонку("СуммаПлатежа");
		СуммаПлатежаНовая = РаботаСКурсамиВалютКлиентСервер.ПересчитатьПоКурсу(
			СуммаПлатежаСтарая, ПараметрыСтарогоКурса, ПараметрыНовогоКурса);
		СуммыПлатежейНовые = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(
			СуммаПлатежаНовая, СуммыПлатежейСтарые);
		ТаблицаГрафикПлатежей.ЗагрузитьКолонку(СуммыПлатежейНовые, "СуммаПлатежа");
		
		Объект.ГрафикПлатежей.Загрузить(ТаблицаГрафикПлатежей);
		
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из Объект[ТабЧасть] Цикл
		СтрокаТаблицы.ОценкаБУ = РаботаСКурсамиВалютКлиентСервер.ПересчитатьПоКурсу(
			СтрокаТаблицы.ОценкаБУ, ПараметрыСтарогоКурса, ПараметрыНовогоКурса);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьРассчитатьСуммыВводОстатков(Форма, ПараметрыСтарогоКурса, ПараметрыНовогоКурса) Экспорт
	
	Объект = Форма.Объект;
	
	СуммаСтарая = Объект.ГрафикПроцентныхРасходов.Итог("СуммаПроцентов");
	
	Если СуммаСтарая > 0 Тогда
		
		ТаблицаГрафик = Объект.ГрафикПроцентныхРасходов.Выгрузить();
		
		СуммыСтарые = ТаблицаГрафик.ВыгрузитьКолонку("СуммаПроцентов");
		СуммаНовая = РаботаСКурсамиВалютКлиентСервер.ПересчитатьПоКурсу(
			СуммаСтарая, ПараметрыСтарогоКурса, ПараметрыНовогоКурса);
		СуммыНовые = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(
			СуммаНовая, СуммыСтарые);
		ТаблицаГрафик.ЗагрузитьКолонку(СуммыНовые, "СуммаПроцентов");
		
		Объект.ГрафикПроцентныхРасходов.Загрузить(ТаблицаГрафик);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
