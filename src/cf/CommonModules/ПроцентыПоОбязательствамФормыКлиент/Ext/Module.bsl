// Содержимое модуля отличается в версиях ПРОФ и КОРП.
// В версии ПРОФ отсутствует:
// - тело экспортных процедур (остаются только сигнатуры методов)
// - неэкспортные процедуры и все функции.
// Поэтому не допускается реализация в модуле экспортных функций: вместо них следует использовать процедуры с возвращаемым параметром.

#Область СлужебныйПрограммныйИнтерфейс

Процедура ОбработкаВыбора(Форма, ВыбранноеЗначение, ИсточникВыбора, СпособОценки) Экспорт
	
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Объект = Форма.Объект;
	
	ТипДанных = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ВыбранноеЗначение, "ТипДанных", "ГрафикПлатежей");
	
	Если ТипДанных = "ГрафикПроцентныхРасходов" Тогда
		
		Объект.ГрафикПроцентныхРасходов.Очистить();
		Для ИндексСтроки = 0 По ВыбранноеЗначение.Даты.ВГраница() Цикл
			НоваяСтрока = Объект.ГрафикПроцентныхРасходов.Добавить();
			НоваяСтрока.ДатаНачисления = ВыбранноеЗначение.Даты[ИндексСтроки];
			НоваяСтрока.СуммаПроцентов = ВыбранноеЗначение.Суммы[ИндексСтроки];
		КонецЦикла;
		ПроцентыПоОбязательствамФормыКлиентСервер.СформироватьНадписьГрафикПроцентныхРасходов(Форма, СпособОценки);
		
	Иначе // "ГрафикПлатежей"
		
		Объект.ГрафикПлатежей.Очистить();
		Для ИндексСтроки = 0 По ВыбранноеЗначение.Даты.ВГраница() Цикл
			НоваяСтрока = Объект.ГрафикПлатежей.Добавить();
			НоваяСтрока.ДатаПлатежа = ВыбранноеЗначение.Даты[ИндексСтроки];
			НоваяСтрока.СуммаПлатежа = ВыбранноеЗначение.Суммы[ИндексСтроки];
		КонецЦикла;
		ПроцентыПоОбязательствамФормыКлиентСервер.СформироватьНадписьГрафикПлатежей(Форма, СпособОценки);
		
	КонецЕсли;
	
	Форма.Модифицированность = Истина;
	
КонецПроцедуры

Процедура ПолучитьПараметрыФормыГрафикПлатежейДляАренды(Форма, ПараметрыФормы) Экспорт
	
	Если Не Форма.ПроверитьДатуОкончанияАренды() Тогда
		Возврат;
	КонецЕсли;
	
	Объект = Форма.Объект;
	
	ПараметрыФормы = ГрафикПлатежейПараметрыФормыОбщие(Форма);
	ПараметрыФормы.Вставить("ДатаОкончания", Объект.ДатаОкончанияАренды);
	
КонецПроцедуры

Процедура ПолучитьПараметрыФормыГрафикПлатежейДляРассрочки(Форма, ПараметрыФормы) Экспорт
	
	ПараметрыФормы = ГрафикПлатежейПараметрыФормыОбщие(Форма);
	ПараметрыФормы.Вставить("ЭтоРассрочка", Истина);
	
КонецПроцедуры

Процедура ПолучитьПараметрыФормыГрафикПроцентныхРасходов(Форма, ПараметрыФормы) Экспорт
	
	ПараметрыФормы = ГрафикПроцентныхРасходовПараметрыФормыОбщие(Форма);
	
КонецПроцедуры

Процедура НадписьГрафикПлатежейНажатие(Форма, СтандартнаяОбработка, ПараметрыФормы) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ЗначениеЗаполнено(ПараметрыФормы) Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("Обработка.ГрафикПлатежейПоДоговору.Форма", ПараметрыФормы, Форма);
	
КонецПроцедуры

Процедура НадписьГрафикПроцентныхРасходовНажатие(Форма, СтандартнаяОбработка, ПараметрыФормы) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ЗначениеЗаполнено(ПараметрыФормы) Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("Документ.ВводНачальныхОстатков.Форма.ФормаГрафикПроцентныхРасходов", ПараметрыФормы, Форма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ГрафикПлатежейПараметрыФормыОбщие(Форма)
	
	Объект = Форма.Объект;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДатаДокумента", Объект.Дата);
	ПараметрыФормы.Вставить("Даты", Новый Массив);
	ПараметрыФормы.Вставить("Суммы", Новый Массив);
	Для Каждого СтрокаТаблицы Из Объект.ГрафикПлатежей Цикл
		ПараметрыФормы.Даты.Добавить(СтрокаТаблицы.ДатаПлатежа);
		ПараметрыФормы.Суммы.Добавить(СтрокаТаблицы.СуммаПлатежа);
	КонецЦикла;
	
	ПараметрыФормы.Вставить("ТолькоПросмотр", Форма.ТолькоПросмотр);
	
	Возврат ПараметрыФормы;
	
КонецФункции

Функция ГрафикПроцентныхРасходовПараметрыФормыОбщие(Форма)
	
	Объект = Форма.Объект;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДатаДокумента", Объект.Дата);
	ПараметрыФормы.Вставить("Валюта", 
		?(Форма.РасчетыВВалюте Или Форма.РасчетыВУЕ, Форма.ВалютаВзаиморасчетов, Неопределено));
	ПараметрыФормы.Вставить("Даты", Новый Массив);
	ПараметрыФормы.Вставить("Суммы", Новый Массив);
	Для Каждого СтрокаТаблицы Из Объект.ГрафикПроцентныхРасходов Цикл
		ПараметрыФормы.Даты.Добавить(СтрокаТаблицы.ДатаНачисления);
		ПараметрыФормы.Суммы.Добавить(СтрокаТаблицы.СуммаПроцентов);
	КонецЦикла;
	
	ПараметрыФормы.Вставить("ТолькоПросмотр", Форма.ТолькоПросмотр);
	
	Возврат ПараметрыФормы;
	
КонецФункции

#КонецОбласти
