////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок = Параметры.Заголовок;
	ПоказатьОтметкуЭлементов = Параметры.ПоказатьОтметкуЭлементов;
	
	Если ЗначениеЗаполнено(Параметры.Ширина) Тогда
		Ширина = Параметры.Ширина;
	КонецЕсли;
	
	Для Каждого ЭлементСписка Из Параметры.Список Цикл
		
		НоваяСтрока = Список.Добавить();
		НоваяСтрока.Значение = ЭлементСписка.Значение;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Параметры.ТекущаяСтрока) Тогда
		
		МассивСтрок = Список.НайтиСтроки(Новый Структура("Значение", Параметры.ТекущаяСтрока));
		
		Если МассивСтрок.Количество() > 0  Тогда
			Элементы.Список.ТекущаяСтрока = Список.Индекс(МассивСтрок[0]);
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.Список.РежимВыбора = Не ПоказатьОтметкуЭлементов;
	Элементы.СписокВыбораОтметка.Видимость = ПоказатьОтметкуЭлементов;
	Элементы.СписокВыбратьОтмеченные.Видимость = ПоказатьОтметкуЭлементов;
	Элементы.СписокВыбратьОтмеченные.КнопкаПоУмолчанию = ПоказатьОтметкуЭлементов;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ <Список>

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Закрыть(Список[Значение].Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьОтмеченные(Команда)
	
	МассивВыбранных = Новый Массив;
	
	Для Каждого Элемент Из Список Цикл
		Если Элемент.Отметка Тогда
			МассивВыбранных.Добавить(Элемент.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Закрыть(МассивВыбранных);
	
КонецПроцедуры
