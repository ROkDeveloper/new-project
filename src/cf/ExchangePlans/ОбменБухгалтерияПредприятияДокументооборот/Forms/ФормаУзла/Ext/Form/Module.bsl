#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Договоры.Видимость = Объект.Договоры.Количество() > 0;
	Элементы.ПлатежныеПоручения.Видимость = Объект.ПлатежныеПоручения.Количество() > 0;
	Если Объект.Договоры.Количество() = 0
		и Объект.ПлатежныеПоручения.Количество() = 0 Тогда
		Элементы.ДекорацияДанныеМогутьБытьДополнены.Заголовок = НСтр(
			"ru = 'Данные, получаемые из Документооборота, могут быть дополнены.
			|Настройка заполнения загружаемых договоров и платежных поручений доступна
			|после настройки состава выгружаемых данных на стороне Документооборота
			|и после выполнения первой синхронизации данных.'");
	КонецЕсли;
	
	ТекущийОбъект = РеквизитФормыВЗначение("Объект");
	
	// договоры
	Для Каждого СтрокаТабличнойЧасти Из ТекущийОбъект.Договоры Цикл
		СтрокаТаблицы = Договоры.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаТабличнойЧасти);
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.ЗначенияРеквизитов) Тогда
			ЗначенияРеквизитов = ЗначениеИзСтрокиВнутр(СтрокаТабличнойЧасти.ЗначенияРеквизитов);
		Иначе
			ЗначенияРеквизитов = Новый Структура;
		КонецЕсли;
		СтрокаТаблицы.АдресВоВременномХранилище =
			ПоместитьВоВременноеХранилище(ЗначенияРеквизитов, Новый УникальныйИдентификатор());
	КонецЦикла;
	
	// платежные поручения
	Для Каждого СтрокаТабличнойЧасти Из ТекущийОбъект.ПлатежныеПоручения Цикл
		СтрокаТаблицы = ПлатежныеПоручения.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаТабличнойЧасти);
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.ЗначенияРеквизитов) Тогда
			ЗначенияРеквизитов = ЗначениеИзСтрокиВнутр(СтрокаТабличнойЧасти.ЗначенияРеквизитов);
		Иначе
			ЗначенияРеквизитов = Новый Структура;
		КонецЕсли;
		СтрокаТаблицы.АдресВоВременномХранилище =
			ПоместитьВоВременноеХранилище(ЗначенияРеквизитов, Новый УникальныйИдентификатор());
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// договоры
	ТекущийОбъект.Договоры.Очистить();
	Для Каждого СтрокаТаблицы Из Договоры Цикл
		СтрокаТабличнойЧасти = ТекущийОбъект.Договоры.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаТаблицы);
		СтрокаТабличнойЧасти.ЗначенияРеквизитов = СтрокаТаблицы.АдресВоВременномХранилище;
	КонецЦикла;
	
	// платежные поручения
	ТекущийОбъект.ПлатежныеПоручения.Очистить();
	Для Каждого СтрокаТаблицы Из ПлатежныеПоручения Цикл
		СтрокаТабличнойЧасти = ТекущийОбъект.ПлатежныеПоручения.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаТаблицы);
		СтрокаТабличнойЧасти.ЗначенияРеквизитов = СтрокаТаблицы.АдресВоВременномХранилище;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ОбменДаннымиСервер.ФормаУзлаПриЗаписиНаСервере(ТекущийОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Оповестить("Запись_УзелПланаОбмена");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДоговорыПредставлениеЗначенийРеквизитовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ЗначенияРеквизитов = ПолучитьИзВременногоХранилища(Элементы.Договоры.ТекущиеДанные.АдресВоВременномХранилище);
	Если ЗначенияРеквизитов = Неопределено Тогда
		ЗначенияРеквизитов = Новый Структура;
	КонецЕсли;
	ПараметрыФормы.Вставить("ЗначенияРеквизитов", ЗначенияРеквизитов);
	ПараметрыФормы.Вставить("ВидДокумента", Элементы.Договоры.ТекущиеДанные.ВидДокумента);
	ПараметрыФормы.Вставить("ТипОбъекта", "Справочник.ДоговорыКонтрагентов");
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ДоговорыЗначенияРеквизитовЗавершениеВыбора", ЭтаФорма,
		Элементы.Договоры.ТекущиеДанные);
	ОткрытьФорму("ПланОбмена.ОбменБухгалтерияПредприятияДокументооборот.Форма.ФормаВыбораЗначенийРеквизитов",
		ПараметрыФормы, ЭтаФорма,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорыЗначенияРеквизитовЗавершениеВыбора(РезультатВыбора, ТекущиеДанные) Экспорт
	
	Если ТипЗнч(РезультатВыбора) = Тип("Структура") Тогда
		ТекущиеДанные.НеВыбраныЗначенияОбязательныхРеквизитов = Истина;
		
		// соберем представление значений реквизитов
		ПредставлениеЗначенийРеквизитов = ""; Разделитель = "";
		Для Каждого КлючИЗначение Из РезультатВыбора Цикл
			ПредставлениеЗначенийРеквизитов = ПредставлениеЗначенийРеквизитов
				+ Разделитель + Строка(КлючИЗначение.Значение);
			Разделитель = "; ";
			Если КлючИЗначение.Ключ = "ВидДоговора" И ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
				ТекущиеДанные.НеВыбраныЗначенияОбязательныхРеквизитов = Ложь;
			КонецЕсли;
		КонецЦикла;
		
		// по обязательному реквизиту - отдельное предупреждение
		Если ТекущиеДанные.НеВыбраныЗначенияОбязательныхРеквизитов Тогда
			ПредставлениеЗначенийРеквизитов = НСтр("ru = 'Вид договора не выбран'") 
				+ Разделитель + ПредставлениеЗначенийРеквизитов;
		КонецЕсли;
		
		ТекущиеДанные.ПредставлениеЗначенийРеквизитов = ПредставлениеЗначенийРеквизитов;
		ПоместитьВоВременноеХранилище(РезультатВыбора, ТекущиеДанные.АдресВоВременномХранилище);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПлатежныеПорученияПредставлениеЗначенийРеквизитовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ЗначенияРеквизитов = ПолучитьИзВременногоХранилища(
		Элементы.ПлатежныеПоручения.ТекущиеДанные.АдресВоВременномХранилище);
	Если ЗначенияРеквизитов = Неопределено Тогда
		ЗначенияРеквизитов = Новый Структура;
	КонецЕсли;
	ПараметрыФормы.Вставить("ЗначенияРеквизитов", ЗначенияРеквизитов);
	ПараметрыФормы.Вставить("ВидДокумента", Элементы.ПлатежныеПоручения.ТекущиеДанные.ВидДокумента);
	ПараметрыФормы.Вставить("ТипОбъекта", "Документ.ПлатежноеПоручение");
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПлатежныеПорученияЗначенияРеквизитовЗавершениеВыбора", ЭтаФорма,
		Элементы.ПлатежныеПоручения.ТекущиеДанные);
	ОткрытьФорму("ПланОбмена.ОбменБухгалтерияПредприятияДокументооборот.Форма.ФормаВыбораЗначенийРеквизитов",
		ПараметрыФормы, ЭтаФорма,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура ПлатежныеПорученияЗначенияРеквизитовЗавершениеВыбора(РезультатВыбора, ТекущиеДанные) Экспорт
	
	Если ТипЗнч(РезультатВыбора) = Тип("Структура") Тогда
		// соберем представление значений реквизитов
		ПредставлениеЗначенийРеквизитов = ""; Разделитель = "";
		Для каждого КлючИЗначение из РезультатВыбора Цикл
			ПредставлениеЗначенийРеквизитов = ПредставлениеЗначенийРеквизитов
				+ Разделитель + Строка(КлючИЗначение.Значение);
			Разделитель = "; ";
		КонецЦикла;
		ТекущиеДанные.ПредставлениеЗначенийРеквизитов = ПредставлениеЗначенийРеквизитов;
		ПоместитьВоВременноеХранилище(РезультатВыбора, ТекущиеДанные.АдресВоВременномХранилище);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти