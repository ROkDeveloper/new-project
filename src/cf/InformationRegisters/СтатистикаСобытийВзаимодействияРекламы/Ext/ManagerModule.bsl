// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Отправить статистику в сервис.
// 
// Параметры:
//  СтатистикаОтправлена - Булево
//
Процедура ОтправитьСтатистикуВСервис(СтатистикаОтправлена = Ложь) Экспорт
	
	ПараметрыЗапроса = ПараметрыЗапросаОтправкаСтатистикиРекламы();
	Если ПараметрыЗапроса.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СписокПрофилей = Новый СписокЗначений(); // СписокЗначений Из Строка
	
	Для Каждого ПараметрЗапроса Из ПараметрыЗапроса Цикл
		
		Дата = ПараметрЗапроса.forDay; // Дата
		
		ПараметрЗапроса.forDay = Формат(Дата, "ДФ=yyyy-MM-dd;"); // Дата
		СписокПрофилей.Добавить(ПараметрЗапроса.profileId);
		
	КонецЦикла;
	
	Данные = РекламныйСервисСлужебный.ЗначениеВJSON(ПараметрыЗапроса);
	
	ХешСуммаЗапроса = ХешСуммаЗапросаСтатистики(СписокПрофилей, Данные);
	
	Запрос = РекламныйСервисСлужебный.ЗапросОтправкаСтатистикиРекламы(Данные, ХешСуммаЗапроса);
	Ответ = РекламныйСервисСлужебный.ВыполнитьЗапрос(Запрос);
	Если Ответ.Отказ Тогда
		
		Если Ответ.Ответ.КодСостояния = 401 Тогда
			Комментарий = НСтр("ru = 'Отсутствует заголовок ""X-Advertising-Content-Key"" с подписью сообщения'", 
				ОбщегоНазначения.КодОсновногоЯзыка());
		ИначеЕсли Ответ.Ответ.КодСостояния = 403 Тогда
			Комментарий = НСтр("ru = 'Подпись в заголовке ""X-Advertising-Content-Key"" не валидная'", 
				ОбщегоНазначения.КодОсновногоЯзыка());
		КонецЕсли;
		
		ШаблонСобытия = НСтр("ru = '%1 Отправка статистики взаимодействия потребителей с рекламой в сервис.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
		ИмяСобытия = СтрШаблон(ШаблонСобытия, РекламныйСервисСлужебный.ИмяСобытияРекламногоСервиса());
		
		ЗаписьЖурналаРегистрации(
			ИмяСобытия, 
			УровеньЖурналаРегистрации.Ошибка, , , 
			Комментарий);
		
		Возврат;
		
	КонецЕсли;
	
	ОчиститьСтатистику();
	
	СтатистикаОтправлена = Истина;
	
КонецПроцедуры

// Добавить событие.
// 
// Измерение "ИдентификаторРекламы" имеет тип ОпределяемыйТип.ИдентификаторРекламы (Строка) 
// потому что справочник Реклама в режиме сервиса не разделенный, 
// и добавление/удаление элементов производится в неразделенном режиме. При этом данный регистр разделенный,
// удаление записей из не разделенного режима не возможно и записи статистики нужны для отправки в сервис 
// после окончания существования рекламы
// 
// Параметры:
//  ПараметрыСобытия см.НовыйПараметрыСобытияВзаимодействия
//
Процедура СоздатьНаборыЗаписей(Знач ПараметрыСобытия) Экспорт
	
	ИдентификаторыРекламы = ПараметрыСобытия.ИдентификаторыРекламы;
	ИмяФормы = ПараметрыСобытия.ИмяФормы;
	СобытиеВзаимодействия = ПараметрыСобытия.СобытиеВзаимодействия;
	Дата = ПараметрыСобытия.Дата;
	ИмяСобытия = РекламныйСервисСлужебный.ИмяСобытияВзаимодействия();
	ПрофильПотребителя = ПараметрыСобытия.ПрофильПотребителя;
	
	НачатьТранзакцию();
	
	Попытка
		
		УстановитьПривилегированныйРежим(Истина);
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СтатистикаСобытийВзаимодействияРекламы");
		ЭлементБлокировки.УстановитьЗначение("Дата", Дата);
		ЭлементБлокировки.УстановитьЗначение("ПрофильПотребителя", ПрофильПотребителя);
		ЭлементБлокировки.УстановитьЗначение("ИмяФормы", ИмяФормы);
		ЭлементБлокировки.УстановитьЗначение("СобытиеВзаимодействия", СобытиеВзаимодействия);
		
		ЭлементБлокировки.ИсточникДанных = ПараметрыСобытия.ИдентификаторыРекламы;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ИдентификаторРекламы", "Идентификатор");
		
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Дата.Установить(Дата);
		НаборЗаписей.Отбор.ПрофильПотребителя.Установить(ПрофильПотребителя);
		НаборЗаписей.Отбор.ИмяФормы.Установить(ИмяФормы);
		НаборЗаписей.Отбор.СобытиеВзаимодействия.Установить(СобытиеВзаимодействия);
		
		Для Каждого СтрокаИдентификатора Из ИдентификаторыРекламы Цикл
			
			ИдентификаторРекламы = 
				РекламныйСервисСлужебный.ПривестиТипКИдентификаторуРекламы(
					СтрокаИдентификатора.Идентификатор); // ОпределяемыйТип.ИдентификаторРекламы
			
			НаборЗаписей.Отбор.ИдентификаторРекламы.Установить(ИдентификаторРекламы);
			НаборЗаписей.Прочитать();
			
			Если НаборЗаписей.Количество() > 0 Тогда
				СтрокаЗаписи = НаборЗаписей[0];
			Иначе
				
				СтрокаЗаписи = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаЗаписи, ПараметрыСобытия);
				СтрокаЗаписи.ИдентификаторРекламы = ИдентификаторРекламы;
				
			КонецЕсли;
			
			СтрокаЗаписи.Количество = СтрокаЗаписи.Количество + 1;
			
			Если НаборЗаписей.ПроверитьЗаполнение() Тогда
				НаборЗаписей.Записать();
			Иначе
				
				Комментарий = НСтр(
					"ru = 'Ошибка проверки заполнения записи регистра ""СтатистикаСобытийВзаимодействияРекламы"".'");
				ЗаписьЖурналаРегистрации(
					ИмяСобытия, 
					УровеньЖурналаРегистрации.Ошибка, , 
					ИдентификаторРекламы, 
					Комментарий);
				
			КонецЕсли;
		
		КонецЦикла;
		
		УстановитьПривилегированныйРежим(Ложь);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		Комментарий = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ЗаписьЖурналаРегистрации(
			ИмяСобытия, 
			УровеньЖурналаРегистрации.Ошибка, , 
			ИдентификаторРекламы, 
			Комментарий);
		
		ВызватьИсключение Комментарий;
		
	КонецПопытки;
	
КонецПроцедуры

// Новый параметры события взаимодействия с рекламными данными.
// 
// Возвращаемое значение:
//  Структура - Новый параметры события:
// * Дата 								- Дата - дата показа без времени
// * ИдентификаторыРекламы 				- см. НовыйТаблицаИдентификаторовРекламы
// * ИмяФормы 							- Строка - полное имя формы вызова.
// * СобытиеВзаимодействия 				- ПеречислениеСсылка.СобытияВзаимодействияРекламы - по умолчанию показ рекламы.
// * ПрофильПотребителя 				- ОпределяемыйТип.ПрофильПотребителяРекламы, Произвольный - профиль потребителя
//
Функция НовыйПараметрыСобытияВзаимодействия() Экспорт
	
	ПараметрыСобытия = Новый Структура;
	ПараметрыСобытия.Вставить("Дата", НачалоДня(ТекущаяДатаСеанса()));
	
	ИдентификаторыРекламы = НовыйТаблицаИдентификаторовРекламы();
	ПараметрыСобытия.Вставить("ИдентификаторыРекламы", ИдентификаторыРекламы);
	
	ПараметрыСобытия.Вставить("ИмяФормы", "");
	ПараметрыСобытия.Вставить("СобытиеВзаимодействия", 
		Перечисления.СобытияВзаимодействияРекламы.ПереходПоСсылке);
	
	ОписаниеТипаПрофиль = РекламныйСервисСлужебный.ПустойПрофильПотребителя();
	ПараметрыСобытия.Вставить("ПрофильПотребителя", ОписаниеТипаПрофиль);
	
	Возврат ПараметрыСобытия;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Конструкторы

// Конструктор параметры запроса отправка статистики рекламы.
// 
// Возвращаемое значение:
//  Структура - Новый параметры запроса отправка статистики рекламы:
// * profileId - Строка - Идентификатор Профиля Потребителя.
// * forDay - Строка, Дата - Формат: "2022-11-02". Дата к которой относится передаваемая статистика.
// * advertisingStatistics - Массив из см. НовыйДанныеАгрегированнойСтатистикиРекламы - Агрегированная статистика.
//
Функция НовыйПараметрЗапросаОтправкиСтатистикиРекламы()

	Результат = Новый Структура;
	Результат.Вставить("profileId", "");
	Результат.Вставить("forDay", "");
	Результат.Вставить("advertisingStatistics", Новый Массив);
	
	Возврат Результат;

КонецФункции

// Конструктор - данные агрегированной статистики рекламы.
// 
// Возвращаемое значение:
//  Структура - Новый данные агрегированной статистики рекламы:
// * advertisingId - Строка - Идентификатор Рекламы.
// * mediaContexts - Массив из см.НовыйДанныеСтатистикиРекламы - Статистика по взаимодействию Потребителя с Рекламой.
//
Функция НовыйДанныеАгрегированнойСтатистикиРекламы()
	
	Результат = Новый Структура;
	Результат.Вставить("advertisingId", "");
	Результат.Вставить("mediaContexts", Новый Массив);
	
	Возврат Результат;
	
КонецФункции

// Конструктор - данные статистики рекламы.
// 
// Возвращаемое значение:
//  Структура - Новый данные статистики рекламы:
// * screenId - Строка - Идентификатор экранной формы.
// * showsNumber - Число - Количество показов Рекламы на экранной форме.
// * actionsNumber - Число - Количество Целевых действий (переходов по Рекламе) на экранной форме.
//
Функция НовыйДанныеСтатистикиРекламы()
	
	Результат = Новый Структура;
	Результат.Вставить("screenId", "");
	Результат.Вставить("showsNumber", 0);
	Результат.Вставить("actionsNumber", 0);
	
	Возврат Результат;
	
КонецФункции

// Новый таблица идентификаторов рекламы.
// 
// Возвращаемое значение:
//  ТаблицаЗначений:
// * Идентификатор - ОпределяемыйТип.ИдентификаторРекламы
//
Функция НовыйТаблицаИдентификаторовРекламы() Экспорт
	
	ИдентификаторыРекламы = Новый ТаблицаЗначений;
	ИдентификаторыРекламы.Колонки.Добавить("Идентификатор");
	
	Возврат ИдентификаторыРекламы;
	
КонецФункции

// Новый данные сопоставления профиля рекламы.
// 
// Возвращаемое значение:
//  Структура -  Новый данные сопоставления профиля рекламы:
// * matchingDay - Строка
// * targets - Массив из см. НовыйТаргетыСопоставления
//
Функция НовыйДанныеСопоставленияПрофиляРекламы()
	
	Результат = Новый Структура;
	Результат.Вставить("matchingDay", "");
	Результат.Вставить("targets", Новый Массив);
	
	Возврат Результат;
	
КонецФункции

// Новый таргеты сопоставления.
// 
// Возвращаемое значение:
//  Структура -  Новый таргеты сопоставления:
// * characteristicId - Произвольный, ОпределяемыйТип.ВидХарактеристикиРекламы - 
// * characteristicValues - Массив из ОпределяемыйТип.ЗначениеХарактеристикиРекламы - 
//
Функция НовыйТаргетыСопоставления()
	
	Результат = Новый Структура;
	Результат.Вставить("characteristicId", Метаданные.ОпределяемыеТипы.ВидХарактеристикиРекламы.Тип.ПривестиЗначение());
	Результат.Вставить("characteristicValues", Новый Массив);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

// Конструктор параметры запроса отправка статистики рекламы.
// 
// Возвращаемое значение:
//  Массив из Структура см. НовыйПараметрЗапросаОтправкиСтатистикиРекламы
//
Функция ПараметрыЗапросаОтправкаСтатистикиРекламы()
	
	ДатаНачала = ОценкаПроизводительности.НачатьЗамерВремени();
	
	ПараметрыЗапроса = Новый Массив; // Массив из см. НовыйПараметрЗапросаОтправкиСтатистикиРекламы
	
	НачатьТранзакцию();
	
	Попытка
		
		ВыборкаПериод = ЗапросОтправкаСтатистики();
		
		Пока ВыборкаПериод.Следующий() Цикл
			
			Дата = ВыборкаПериод.Дата;
			
			ВыборкаПрофиль = ВыборкаПериод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаПрофиль.Следующий() Цикл
				
				ПараметрЗапроса = НовыйПараметрЗапросаОтправкиСтатистикиРекламы();
				ПараметрЗапроса.forDay = Дата;
				
				// Идентификатор профиля
				ПрофильПотребителя = ВыборкаПрофиль.ПрофильПотребителя; // ОпределяемыйТип.ПрофильПотребителяРекламы
				ИдПрофильПотребителя = 
					РекламныйСервисСлужебный.ИдентификаторПользователяИБПоПрофилюПотребителя(ПрофильПотребителя);
				
				ОбщегоНазначенияКлиентСервер.Проверить(
					ТипЗнч(ИдПрофильПотребителя) = Тип("Строка"), 
					НСтр("ru = 'Тип идентификатора профиля потребителя не является строковым.'"), 
					"РекламныйСервисСлужебный.ПараметрыЗапросаОтправкаСтатистикиРекламы");
				
				ПараметрЗапроса.profileId = ИдПрофильПотребителя;
				
				// Агрегированная статистика
				ВыборкаИдентификаторРекламы = ВыборкаПрофиль.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаИдентификаторРекламы.Следующий() Цикл
					
					ДобавленныеВидыХарактеристик = Новый Массив;
					ДанныеАгрегированнойСтатистики = НовыйДанныеАгрегированнойСтатистикиРекламы();
					
					ИдентификаторРекламы = ВыборкаИдентификаторРекламы.ИдентификаторРекламы; // ОпределяемыйТип.ИдентификаторРекламы
					ДанныеАгрегированнойСтатистики.advertisingId = ИдентификаторРекламы;
					
					ВыборкаФорма = ВыборкаИдентификаторРекламы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ВыборкаФорма.Следующий() Цикл
						
						ДанныеСтатистики = НовыйДанныеСтатистикиРекламы();
						
						ИмяФормы = ВыборкаФорма.ИмяФормы; // Строка
						ДанныеСтатистики.screenId = ИмяФормы;
						
						ВыборкаСобытия = ВыборкаФорма.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Пока ВыборкаСобытия.Следующий() Цикл
							
							Количество = ВыборкаСобытия.Количество; // Число
							Если ВыборкаСобытия.СобытиеВзаимодействия = 
									Перечисления.СобытияВзаимодействияРекламы.ПереходПоСсылке Тогда
								ДанныеСтатистики.actionsNumber = Количество;
							ИначеЕсли ВыборкаСобытия.СобытиеВзаимодействия = 
									Перечисления.СобытияВзаимодействияРекламы.ПоказРекламы Тогда 
								ДанныеСтатистики.showsNumber = Количество;
							КонецЕсли;
							
							// Данные сопоставления профиля потребителя и рекламы
							ВыборкаИдентификаторСопоставления = ВыборкаСобытия.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
							Пока ВыборкаИдентификаторСопоставления.Следующий() Цикл
								
								// У рекламы может не быть таргетов.
								Если ВыборкаИдентификаторСопоставления.ДатаСопоставления = Дата(1, 1, 1) Тогда
									Продолжить;
								КонецЕсли;
								
								Если Не ДанныеАгрегированнойСтатистики.Свойство("matchingInfo") Тогда
									
									ДанныеАгрегированнойСтатистики.Вставить("matchingInfo", НовыйДанныеСопоставленияПрофиляРекламы());
									ДатаСопоставления = Формат(ВыборкаИдентификаторСопоставления.ДатаСопоставления, "ДФ=yyyy-MM-dd;");
									ДанныеАгрегированнойСтатистики.matchingInfo.matchingDay = ДатаСопоставления;
									
								КонецЕсли;
								
								ВыборкаВидХарактеристики = ВыборкаИдентификаторСопоставления.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
								Пока ВыборкаВидХарактеристики.Следующий() Цикл
									
									ВидХарактеристикиРекламы = ВыборкаВидХарактеристики.ВидХарактеристикиРекламы;
									
									Если ДобавленныеВидыХарактеристик.Найти(ВидХарактеристикиРекламы) = Неопределено Тогда
										
										ТаргетыСопоставления = НовыйТаргетыСопоставления();
										
										ДобавленныеВидыХарактеристик.Добавить(ВидХарактеристикиРекламы);
										ТаргетыСопоставления.characteristicId = ВидХарактеристикиРекламы;
										
										ВыборкаЗначения = ВыборкаВидХарактеристики.Выбрать();
										Пока ВыборкаЗначения.Следующий() Цикл
											
											ЗначениеХарактеристики = ВыборкаЗначения.ЗначениеХарактеристики;
											ТаргетыСопоставления.characteristicValues.Добавить(ЗначениеХарактеристики);
											
										КонецЦикла;
										
										ДанныеАгрегированнойСтатистики.matchingInfo.targets.Добавить(ТаргетыСопоставления);
										
									КонецЕсли;
									
								КонецЦикла;
								
							КонецЦикла;
							
						КонецЦикла;
						
						ДанныеАгрегированнойСтатистики.mediaContexts.Добавить(ДанныеСтатистики);
						
					КонецЦикла;
					
					ПараметрЗапроса.advertisingStatistics.Добавить(ДанныеАгрегированнойСтатистики);
					
					ДобавленныеВидыХарактеристик.Очистить();
					
				КонецЦикла;
				
				ПараметрыЗапроса.Добавить(ПараметрЗапроса);
				
			КонецЦикла;
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ИмяСобытия = РекламныйСервисСлужебный.ИмяСобытияОтправкиСтатистикиВзаимодействия();
		
		ЗаписьЖурналаРегистрации(
			ИмяСобытия, 
			УровеньЖурналаРегистрации.Ошибка, , , 
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
	
	ОценкаПроизводительности.ЗакончитьЗамерВремени(
		НСтр("ru = 'РекламныйСервис: ПараметрыЗапросаОтправкаСтатистикиРекламы'", ОбщегоНазначения.КодОсновногоЯзыка()), 
		ДатаНачала);
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

// Запрос отправка статистики.
// 
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса:
//   * Дата - Дата
//   * ПрофильПотребителя - ОпределяемыйТип.ПрофильПотребителяРекламы
//   * ИдентификаторРекламы - ОпределяемыйТип.ИдентификаторРекламы
//   * ИмяФормы - Строка
//   * СобытиеВзаимодействия - ПеречислениеСсылка.СобытияВзаимодействияРекламы
//   * Количество - ОпределяемыйТип.КоличествоПоказовРекламы
//   * ВидХарактеристикиРекламы - ОпределяемыйТип.ВидХарактеристикиРекламы
//   * ЗначениеХарактеристики - ОпределяемыйТип.ЗначениеХарактеристикиРекламы
//   * ДатаСопоставления - Дата
//
Функция ЗапросОтправкаСтатистики()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СтатистикаСобытийВзаимодействияРекламы.Дата КАК Дата,
		|	СтатистикаСобытийВзаимодействияРекламы.ПрофильПотребителя КАК ПрофильПотребителя,
		|	СтатистикаСобытийВзаимодействияРекламы.ИдентификаторРекламы КАК ИдентификаторРекламы,
		|	СтатистикаСобытийВзаимодействияРекламы.ИмяФормы КАК ИмяФормы,
		|	СтатистикаСобытийВзаимодействияРекламы.СобытиеВзаимодействия КАК СобытиеВзаимодействия,
		|	СтатистикаСобытийВзаимодействияРекламы.Количество КАК Количество,
		|	ЕСТЬNULL(ЗначенияХарактеристикПрофиля.ВидХарактеристикиРекламы, """") КАК ВидХарактеристикиРекламы,
		|	ЕСТЬNULL(ЗначенияХарактеристикПрофиля.ЗначениеХарактеристики, """") КАК ЗначениеХарактеристики,
		|	ЕСТЬNULL(ЗначенияХарактеристикПрофиля.ДатаСопоставления, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаСопоставления
		|ИЗ
		|	РегистрСведений.СтатистикаСобытийВзаимодействияРекламы КАК СтатистикаСобытийВзаимодействияРекламы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияХарактеристикПрофиля КАК ЗначенияХарактеристикПрофиля
		|		ПО СтатистикаСобытийВзаимодействияРекламы.Дата = ЗначенияХарактеристикПрофиля.Период
		|		И ЗначенияХарактеристикПрофиля.ПрофильПотребителяРекламы = СтатистикаСобытийВзаимодействияРекламы.ПрофильПотребителя
		|		И ЗначенияХарактеристикПрофиля.ИдентификаторРекламы = СтатистикаСобытийВзаимодействияРекламы.ИдентификаторРекламы
		|ГДЕ
		|	СтатистикаСобытийВзаимодействияРекламы.Дата < &НачалоТекущегоДня
		|ИТОГИ
		|	МАКСИМУМ(Количество)
		|ПО
		|	Дата,
		|	ПрофильПотребителя,
		|	ИдентификаторРекламы,
		|	ИмяФормы,
		|	СобытиеВзаимодействия,
		|	ДатаСопоставления,
		|	ВидХарактеристикиРекламы";
	
	Запрос.УстановитьПараметр("НачалоТекущегоДня", НачалоДня(ТекущаяДатаСеанса()));
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СтатистикаСобытийВзаимодействияРекламы");
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Дата", "Дата");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	ВыборкаПериод = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Возврат ВыборкаПериод;
	
КонецФункции

// Очистить статистику взаимодействий рекламы за предыдущие дни.
//
Процедура ОчиститьСтатистику()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СтатистикаСобытийВзаимодействияРекламы.Дата
		|ИЗ
		|	РегистрСведений.СтатистикаСобытийВзаимодействияРекламы КАК СтатистикаСобытийВзаимодействияРекламы
		|ГДЕ
		|	СтатистикаСобытийВзаимодействияРекламы.Дата < &ТекущаяДата";
	
	Запрос.УстановитьПараметр("ТекущаяДата", НачалоДня(ТекущаяДатаСеанса()));
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ОчиститьСтатистикуЗаДень(ВыборкаДетальныеЗаписи.Дата);
	КонецЦикла;
	
КонецПроцедуры

// Очистить статистику за день.
// 
// Параметры:
//  Дата - Дата
//
Процедура ОчиститьСтатистикуЗаДень(Знач Дата)
	
	НачатьТранзакцию();
	
	Попытка
		
		УстановитьПривилегированныйРежим(Истина);
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СтатистикаСобытийВзаимодействияРекламы");
		ЭлементБлокировки.УстановитьЗначение("Дата", Дата);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Дата.Установить(Дата);
		
		НаборЗаписей.Записать();
		
		УстановитьПривилегированныйРежим(Ложь);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ЗаписьЖурналаРегистрации(
			РекламныйСервисСлужебный.ИмяСобытияОтправкиСтатистикиВзаимодействия(), 
			УровеньЖурналаРегистрации.Ошибка, , , 
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Хеш сумма запроса статистики.
// 
// Параметры:
//  СписокПрофилей - СписокЗначений из Строка
//  Данные - Строка
// 
// Возвращаемое значение:
//  Строка -  Хеш сумма запроса статистики
//
Функция ХешСуммаЗапросаСтатистики(СписокПрофилей, Данные)
	
	СписокПрофилей.СортироватьПоЗначению();
	СписокПрофилейСтрокой = СтрСоединить(СписокПрофилей.ВыгрузитьЗначения(), ";");
	КлючШифрования = СтрШаблон("%1%2", СписокПрофилейСтрокой, "1C Enterprise");
	
	Хеширование = Новый ХешированиеДанных(ХешФункция.SHA256);
	Хеширование.Добавить(КлючШифрования);
	Хеширование.Добавить(Данные);
	ХешСуммаСтрока = Base64Строка(Хеширование.ХешСумма);
	
	Возврат ХешСуммаСтрока;
	
КонецФункции

#КонецОбласти

#КонецЕсли
