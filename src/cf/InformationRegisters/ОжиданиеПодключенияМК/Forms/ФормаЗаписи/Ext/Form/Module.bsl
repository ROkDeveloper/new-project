
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АдресКартинкиQRКодНастроек = СформироватьQRПодключенияМобильнойКассы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьНастроекМК(Команда)
	
	ТабДокумент = ПечатьНастроекМКНаСервере();
	ТабДокумент.Показать("Настройки подключения");
	
КонецПроцедуры

&НаСервере
Функция ПечатьНастроекМКНаСервере()
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ОтображатьЗаголовки = Ложь;
	ТабДокумент.ОтображатьСетку = Ложь;
	ТабДокумент.Защита = Истина;
	
	Макет = Обработки.ПодключенныеКассы.ПолучитьМакет("ПараметрыПодключения");
	
	Область = Макет.ПолучитьОбласть("Настройки");
	
	ПараметрыТабДок = Новый Структура;
	ПараметрыТабДок.Вставить("Заголовок", НСтр("ru = 'Помощник подключения кассы'"));
	ПараметрыТабДок.Вставить("Организация", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Организация: ""%1"", Склад: ""%2""", Запись.Организация, Запись.Склад));
	ПараметрыТабДок.Вставить("ПараметрыПодключения", Запись.ТекстНастройкиПодключения);
	
	Область.Параметры.Заполнить(ПараметрыТабДок);
	ТабДокумент.Вывести(Область);
	
	ТабДокумент.Рисунки.QRКод.Картинка = Новый Картинка(ПолучитьИзВременногоХранилища(АдресКартинкиQRКодНастроек));
	ТабДокумент.Рисунки.QRКод.ЦветФона = Новый Цвет();
	ТабДокумент.Рисунки.QRКод.Линия    = Новый Линия(ТипЛинииРисункаТабличногоДокумента.НетЛинии);
	
	Возврат ТабДокумент;
	
КонецФункции

&НаСервере
Функция СформироватьQRПодключенияМобильнойКассы() Экспорт
	
	ДанныеQR = Запись.ТекстQRКода;
	
	ПараметрыШтрихкода = ГенерацияШтрихкода.ПараметрыГенерацииШтрихкода();
	ПараметрыШтрихкода.Вставить("Ширина", 200);
	ПараметрыШтрихкода.Вставить("Высота", 200);
	ПараметрыШтрихкода.Вставить("Штрихкод", СокрЛП(ДанныеQR));
	ПараметрыШтрихкода.Вставить("ТипКода", 16);
	ПараметрыШтрихкода.Вставить("ОтображатьТекст", Ложь);
	
	Картинка = ГенерацияШтрихкода.ИзображениеШтрихкода(ПараметрыШтрихкода);
	ДвоичныеДанные = Картинка.ДвоичныеДанные;
	АдресКартинки = ПоместитьВоВременноеХранилище(ДвоичныеДанные, Новый УникальныйИдентификатор());
	
	Возврат АдресКартинки;
	
КонецФункции

