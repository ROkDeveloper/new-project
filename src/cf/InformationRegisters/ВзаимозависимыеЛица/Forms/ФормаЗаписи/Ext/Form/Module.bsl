#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(Запись, Параметры.ЗначенияЗаполнения);
	
	Если Параметры.ЗначенияЗаполнения.Свойство("Организация") Тогда 
		Элементы.Организация.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект,
		ПолучитьДополнительныеСведения(Запись.Организация, Запись.Контрагент, Запись.Период));
		
	ДоступностьКлючевыхЗаписей = Истина;
	Если Параметры.Свойство("ДоступностьКлючевыхЗаписей") Тогда
		ДоступностьКлючевыхЗаписей = Параметры.ДоступностьКлючевыхЗаписей;
	КонецЕсли;
	
	Элементы.Организация.ТолькоПросмотр = НЕ ДоступностьКлючевыхЗаписей;
	Элементы.Контрагент.ТолькоПросмотр = НЕ ДоступностьКлючевыхЗаписей;
	Элементы.Период.ТолькоПросмотр = НЕ ДоступностьКлючевыхЗаписей;
	
	Элементы.ТипВзаимозависимости.АвтоОтметкаНезаполненного = НЕ ДоступностьКлючевыхЗаписей;
	
	УстановитьВидимостьЭлементовНалогообложения();
	
	УстановитьДоступностьЭлементовНалогаНаПрибыль(ЭтотОбъект);
	
	КонтролируемыеСделки.ЗаполнитьСписокГоловныхОрганизаций(Элементы.Организация.СписокВыбора);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "УчастникКонтролируемойСделкиЗаписан" Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект,
			ПолучитьДополнительныеСведения(Запись.Организация, Запись.Контрагент, Запись.Период));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект,
		ПолучитьДополнительныеСведения(Запись.Организация, Запись.Контрагент, Запись.Период));
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект,
		ПолучитьДополнительныеСведения(Запись.Организация, Запись.Контрагент, Запись.Период));
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	ПриИзмененииПериодаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЯвляетсяПлательщикомНалогаНаПрибыльПриИзменении(Элемент)
	
	Если НЕ Запись.ЯвляетсяПлательщикомНалогаНаПрибыль Тогда
		Запись.ЗарегистрированВОЭЗ = Ложь;
		Запись.ПрименяетЛьготыУчастникаРегиональногоИнвестиционногоПроекта = Ложь;
		Запись.ПрименяетИнвестиционныйВычетПоНалогуНаПрибыль = Ложь;
	КонецЕсли;
	УстановитьДоступностьЭлементовНалогаНаПрибыль(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриИзмененииПериодаНаСервере()
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект,
		ПолучитьДополнительныеСведения(Запись.Организация, Запись.Контрагент, Запись.Период));
	
	УстановитьВидимостьЭлементовНалогообложения();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДополнительныеСведения(Организация, Контрагент, Период)
	
	ДополнительныеСведения = Новый Структура("СведенияДействуютПо");
	
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("Организация", Организация);
	Запрос.Параметры.Вставить("Контрагент", Контрагент);
	Запрос.Параметры.Вставить("Период", Период);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(ВзаимозависимыеЛица.Период) КАК Период
	|ИЗ
	|	РегистрСведений.ВзаимозависимыеЛица КАК ВзаимозависимыеЛица
	|ГДЕ
	|	ВзаимозависимыеЛица.Организация = &Организация
	|	И ВзаимозависимыеЛица.Контрагент = &Контрагент
	|	И ВзаимозависимыеЛица.Период > &Период";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Если Выборка.Период = NULL Тогда
		СведенияДействуютПо = НСтр("ru = ''");
	Иначе
		СведенияДействуютПо = НСтр("ru = 'по %ДатаОкончанияДействия%'");
		СведенияДействуютПо = СтрЗаменить(СведенияДействуютПо, "%ДатаОкончанияДействия%", Формат(НачалоДня(Выборка.Период - 1),"ДФ=dd.MM.yyyy"));
		СведенияДействуютПо = СведенияДействуютПо;
	КонецЕсли;
	ДополнительныеСведения.СведенияДействуютПо = СведенияДействуютПо;
	
	Возврат(ДополнительныеСведения);
	
КонецФункции

&НаСервере
Процедура УстановитьВидимостьЭлементовНалогообложения()
	Элементы.ЯвляетсяПлательщикомЕНВД.Видимость = Запись.Период < КонтролируемыеСделки.ДатаОтказаОтЕНВД();
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьЭлементовНалогаНаПрибыль(Форма)
	
	ПлательщикНалогаНаПрибыль = Форма.Запись.ЯвляетсяПлательщикомНалогаНаПрибыль;
	
	Форма.Элементы.ЗарегистрированВОЭЗ.Доступность = ПлательщикНалогаНаПрибыль;
	Форма.Элементы.ПрименяетЛьготыУчастникаРегиональногоИнвестиционногоПроекта.Доступность = ПлательщикНалогаНаПрибыль;
	Форма.Элементы.ПрименяетИнвестиционныйВычетПоНалогуНаПрибыль.Доступность = ПлательщикНалогаНаПрибыль;
	
КонецПроцедуры

#КонецОбласти