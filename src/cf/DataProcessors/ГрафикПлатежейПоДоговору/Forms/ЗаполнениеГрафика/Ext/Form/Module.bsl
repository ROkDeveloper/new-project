#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДатаОкончания = Параметры.ДатаОкончания;
	ДатаДокумента = Параметры.ДатаДокумента;
	
	ТолькоПросмотр = Параметры.ТолькоПросмотр;
	
	ДатаПервогоПлатежа = ДатаДокумента;
	ДатаПоследнегоПлатежа = ДатаОкончания;
	
	ПоказатьПодсказку(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПервогоПлатежаПриИзменении(Элемент)
	
	ПоказатьПодсказку(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	
	Отказ = Не ПроверитьЗаполнение();
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ДатаПоследнегоПлатежа <= ДатаПервогоПлатежа Тогда
		ТекстСообщения = НСтр("ru='Дата последнего платежа должна быть позже даты первого платежа'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ДатаПоследнегоПлатежа", , Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("СуммаПлатежа", СуммаПлатежа);
	Результат.Вставить("ДатаПервогоПлатежа", ДатаПервогоПлатежа);
	Результат.Вставить("ДатаПоследнегоПлатежа", ДатаПоследнегоПлатежа);
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ПоказатьПодсказку(Форма)
	
	ТекстПодсказки = СтрШаблон(НСтр("ru='Первый платеж %1, далее ежемесячно в этот день'"),
		Формат(Форма.ДатаПервогоПлатежа, "ДЛФ=D"));
	Форма.Элементы.Подсказка.Заголовок = ТекстПодсказки;
	
КонецПроцедуры

#КонецОбласти
