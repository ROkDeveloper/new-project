#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДатаОкончания = Параметры.ДатаОкончания;
	ДатаДокумента = Параметры.ДатаДокумента;
	
	Если ЗначениеЗаполнено(Параметры.Даты) Тогда
		Для ИндексПериода = 0 По Параметры.Даты.ВГраница() Цикл
			НоваяСтрока = ГрафикПлатежей.Добавить();
			НоваяСтрока.ДатаПлатежа = Параметры.Даты[ИндексПериода];
			НоваяСтрока.СуммаПлатежа = Параметры.Суммы[ИндексПериода];
		КонецЦикла;
	КонецЕсли;
	
	ТолькоПросмотр = Параметры.ТолькоПросмотр;
	
	ЭтоРассрочка = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ЭтоРассрочка", Ложь);
	
	Элементы.ГрафикПлатежейЗаполнить.Видимость = Не ЭтоРассрочка;
	Элементы.ЗаголовокГрафикПлатежей.Видимость = Не ЭтоРассрочка;
	Элементы.ГрафикПлатежей.ОтображениеПодсказки = ?(ЭтоРассрочка, 
		ОтображениеПодсказки.ОтображатьСверху, 
		ОтображениеПодсказки.Нет);
	
	ВсегоПлатежей = ГрафикПлатежей.Итог("СуммаПлатежа");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ГрафикПлатежей.Очистить();
	
	ДатаПлатежа = ВыбранноеЗначение.ДатаПервогоПлатежа;
	ДеньПлатежа = День(ДатаПлатежа);
	Пока ДатаПлатежа <= ВыбранноеЗначение.ДатаПоследнегоПлатежа Цикл
		НоваяСтрока = ГрафикПлатежей.Добавить();
		НоваяСтрока.ДатаПлатежа = ДатаПлатежа;
		НоваяСтрока.СуммаПлатежа = ВыбранноеЗначение.СуммаПлатежа;
		ДатаПлатежа = ДобавитьМесяц(ДатаПлатежа, 1);
		ДеньСледующегоПлатежа = День(ДатаПлатежа);
		Если ДеньСледующегоПлатежа <> ДеньПлатежа Тогда
			ДеньСледующегоПлатежа = Мин(ДеньПлатежа, День(КонецМесяца(ДатаПлатежа)));
			ДатаПлатежа = Дата(Год(ДатаПлатежа), Месяц(ДатаПлатежа), ДеньСледующегоПлатежа); 
		КонецЕсли;
	КонецЦикла;
	
	ВсегоПлатежей = ГрафикПлатежей.Итог("СуммаПлатежа");
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не Модифицированность Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Перенести изменения в документ?'");
	
	Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыГрафикПлатежей

&НаКлиенте
Процедура ГрафикПлатежейПриИзменении(Элемент)
	ВсегоПлатежей = ГрафикПлатежей.Итог("СуммаПлатежа");
КонецПроцедуры

&НаКлиенте
Процедура ГрафикПлатежейПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ГрафикПлатежей.ТекущиеДанные;
	Если Не ЗначениеЗаполнено(ТекущиеДанные.ДатаПлатежа)
		Или Не ЗначениеЗаполнено(ТекущиеДанные.СуммаПлатежа) Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГрафикПлатежейПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ГрафикПлатежей.Сортировать("ДатаПлатежа");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДатаОкончания", ДатаОкончания);
	ПараметрыФормы.Вставить("ДатаДокумента", ДатаДокумента);
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ТолькоПросмотр", ТолькоПросмотр);
	ОткрытьФорму("Обработка.ГрафикПлатежейПоДоговору.Форма.ЗаполнениеГрафика", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ПеренестиИзменения();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВопросПередЗакрытиемЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ПеренестиИзменения();
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиИзменения()
	
	Результат = Новый Структура;
	Результат.Вставить("Даты", Новый Массив);
	Результат.Вставить("Суммы", Новый Массив);
	Для каждого СтрокаТаблицы Из ГрафикПлатежей Цикл
		Результат.Даты.Добавить(СтрокаТаблицы.ДатаПлатежа);
		Результат.Суммы.Добавить(СтрокаТаблицы.СуммаПлатежа);
	КонецЦикла;
	
	Модифицированность = Ложь;
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры

#КонецОбласти

