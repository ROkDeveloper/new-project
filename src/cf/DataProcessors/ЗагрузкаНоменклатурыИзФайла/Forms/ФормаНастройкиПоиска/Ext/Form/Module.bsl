
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПодготовитьформуНаСервере(Параметры);
	УстановитьУсловноеОформление();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	КоличествоПомеченных = 0;
	Для Каждого ЭлементСписка Из ПоляПоиска Цикл
		КоличествоПомеченных = КоличествоПомеченных + ?(ЭлементСписка.Пометка, 1, 0);
	КонецЦикла;
	
	Если КоличествоПомеченных = 0 Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Необходимо отметить хотя бы одно поле для поиска'"),, "ПоляПоиска",, Отказ); 
	КонецЕсли;
	
	Если КоличествоПомеченных = 1 И ПоляПоиска.НайтиПоЗначению(ЗначениеЭлементаУИД()) <> Неопределено Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Кроме поля ""Уникальный идентификатор"" необходимо отметить хотя бы одно поле для поиска'"),, "ПоляПоиска",, Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПоляПоискаПриИзменении(Элемент)
	
	ПроверитьИзменениеЭлементаУИД();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		Закрыть(ПоляПоиска);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьформуНаСервере(ПараметрыФормы)
	
	ПоляПоиска = ПараметрыФормы.ПоляПоиска.Скопировать();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПоляПоиска");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ПоляПоиска.Значение", ВидСравненияКомпоновкиДанных.Содержит, ЗначениеЭлементаУИД());
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь);
	
	ЖирныйШрифт = Новый Шрифт(,, Истина);
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПоляПоиска");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ПоляПоиска.Значение", ВидСравненияКомпоновкиДанных.Содержит, ЗначениеЭлементаУИД());
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Шрифт", ЖирныйШрифт);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.ПоляПоискаРасширеннаяПодсказка.Заголовок = ТекстПодсказкиНастройкиПоиска(Форма.ПоляПоиска);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ТекстПодсказкиНастройкиПоиска(ПоляПоиска)
	
	НастройкиПоиска = НастройкиПолейПоиска(ПоляПоиска);
	ЕстьПолеУИД = (ПоляПоиска.НайтиПоЗначению(ЗначениеЭлементаУИД()) <> Неопределено);
	
	ЧастиТекста = Новый Массив;
	Если НастройкиПоиска.КоличествоПомеченных > 1 Тогда
		ЧастиТекста.Добавить(Нстр("ru = 'Для поиска номенклатуры используются поля:'"));
		ЧастиТекста.Добавить(Символы.ПС);
		Для Счетчик = 1 По НастройкиПоиска.КоличествоПомеченных Цикл
			Если Счетчик > 1 Тогда
				ЧастиТекста.Добавить(Символы.ПС);
			КонецЕсли;
			ЧастиТекста.Добавить(Формат(Счетчик, "ЧГ=0"));
			ЧастиТекста.Добавить(".");
			ЧастиТекста.Добавить(" ");
			Если Счетчик = 1 И НастройкиПоиска.ПомеченныеПоля[0].Значение = ЗначениеЭлементаУИД() Тогда
			    ЧастиТекста.Добавить("Только");
				ЧастиТекста.Добавить(" ");
				ЧастиТекста.Добавить(НастройкиПоиска.ПомеченныеПоля[0].Представление);
			ИначеЕсли Счетчик = НастройкиПоиска.КоличествоПомеченных Тогда
				ЧастиТекста.Добавить("Только");
				ЧастиТекста.Добавить(" ");
				ЧастиТекста.Добавить(НастройкиПоиска.ПомеченныеПоля[?(ЕстьПолеУИД, 1, 0)].Представление);
			Иначе
				ПервыйЭлемент = Истина;
				Для Индекс = 0 По НастройкиПоиска.КоличествоПомеченных - Счетчик + ?(ЕстьПолеУИД, 1, 0) Цикл
					Если НастройкиПоиска.ПомеченныеПоля[Индекс].Значение = ЗначениеЭлементаУИД() Тогда
						Продолжить;
					КонецЕсли;
					Если Не ПервыйЭлемент Тогда
						ЧастиТекста.Добавить(" ");
						ЧастиТекста.Добавить("+");
						ЧастиТекста.Добавить(" ");
					КонецЕсли;
					ЧастиТекста.Добавить(НастройкиПоиска.ПомеченныеПоля[Индекс].Представление);
					ПервыйЭлемент = Ложь;
				КонецЦикла;
			КонецЕсли;
			Если Счетчик > 1 Или Счетчик = НастройкиПоиска.КоличествоПомеченных Тогда
				ЧастиТекста.Добавить(",");
				ЧастиТекста.Добавить(" ");
				ЧастиТекста.Добавить("если на предыдущем шаге ничего не найдено");
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли НастройкиПоиска.КоличествоПомеченных = 1 Тогда
		ЧастиТекста.Добавить(Нстр("ru = 'Для поиска номенклатуры используется поле'"));
		ЧастиТекста.Добавить(" ");
		ЧастиТекста.Добавить(НастройкиПоиска.ПомеченныеПоля[0].Представление);
	Иначе
		ЧастиТекста.Добавить(Нстр("ru = 'Поля для поиска номенклатуры не указаны'"));
	КонецЕсли;
	
	Возврат СтрСоединить(ЧастиТекста);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НастройкиПолейПоиска(ПоляПоиска)
	
	НастройкиПоиска = Новый Структура;
	НастройкиПоиска.Вставить("КоличествоПомеченных", 0);
	НастройкиПоиска.Вставить("ПомеченныеПоля", Новый Массив);
	Для Каждого ЭлементСписка Из ПоляПоиска Цикл
		Если Не ЭлементСписка.Пометка Тогда
			Продолжить;
		КонецЕсли;
		НастройкиПоиска.КоличествоПомеченных = НастройкиПоиска.КоличествоПомеченных + 1;
		НастройкиПоиска.ПомеченныеПоля.Добавить(ЭлементСписка);
	КонецЦикла;
	
	Возврат НастройкиПоиска;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗначениеЭлементаУИД()
	
	Возврат "УникальныйИдентификатор";
	
КонецФункции

&НаКлиенте
Процедура ПроверитьИзменениеЭлементаУИД()
	
	ЭлементУИД = ПоляПоиска.НайтиПоЗначению(ЗначениеЭлементаУИД());
	Если ЭлементУИД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементУИД.Пометка = Истина;
	
	Пока ПоляПоиска.Индекс(ЭлементУИД) > 0 Цикл
		ПоляПоиска.Сдвинуть(ЭлементУИД, -1);
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти