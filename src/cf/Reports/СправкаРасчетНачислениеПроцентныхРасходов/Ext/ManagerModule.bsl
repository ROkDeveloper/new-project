#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура НастроитьВариантыОтчета(Настройки, ОписаниеОтчета) Экспорт
	
	ВариантыНастроек = ВариантыНастроек();
	Для Каждого Вариант Из ВариантыНастроек Цикл
		ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, Вариант.Имя).Размещение.Вставить(
			Метаданные.Подсистемы.Отчеты.Подсистемы.СправкиРасчеты.Подсистемы.БухгалтерскийУчет, "");
		ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, Вариант.Имя).Размещение.Вставить(
			Метаданные.Подсистемы.Отчеты.Подсистемы.СправкиРасчеты.Подсистемы.БухгалтерскийИНалоговыйУчет, "");
	КонецЦикла;
	
КонецПроцедуры

//Процедура используется подсистемой варианты отчетов
//
Процедура НастройкиОтчета(Настройки) Экспорт
	
	ВариантыНастроек = ВариантыНастроек();
	Для Каждого Вариант Из ВариантыНастроек Цикл
		Настройки.ОписаниеВариантов.Вставить(Вариант.Имя,Вариант.Представление);
	КонецЦикла;
	
КонецПроцедуры

Функция ВариантыНастроек() Экспорт
	
	Массив = Новый Массив;
	
	Массив.Добавить(Новый Структура("Имя, Представление",
		"НачислениеПроцентныхРасходов", "Начисление процентных расходов"));
	
	Возврат Массив;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// В процедуре можно доработать компоновщик перед выводом в отчет. Изменения сохранены не будут.
//
// Параметры:
//  ПараметрыОтчета - Структура - см. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//  Схема        - СхемаКомпоновкиДанных - описание получаемых данных.
//  КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - связь настроек компоновки данных и схемы компоновки.
//
Процедура ПередКомпоновкойМакета(ПараметрыОтчета, Схема, КомпоновщикНастроек) Экспорт
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.КонецПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек, "НачалоПериода", НачалоМесяца(ПараметрыОтчета.КонецПериода));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.КонецПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек, "КонецПериода", КонецДня(ПараметрыОтчета.КонецПериода));
	КонецЕсли;
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек, "ИспользоватьВалютныйУчет", ПолучитьФункциональнуюОпцию("ИспользоватьВалютныйУчет"));
	
	ОписаниеОтбораПоОрганизации = Новый Структура;
	ОписаниеОтбораПоОрганизации.Вставить("Организация", ПараметрыОтчета.Организация);
	ОписаниеОтбораПоОрганизации.Вставить("ВключатьОбособленныеПодразделения", Ложь);
	
	БухгалтерскиеОтчетыВызовСервера.ДобавитьОтборПоОрганизации(ОписаниеОтбораПоОрганизации, КомпоновщикНастроек);
	
КонецПроцедуры

Процедура ПриВыводеЗаголовка(Контекст, КомпоновщикНастроек, Результат) Экспорт
	
	Контекст.НачалоПериода = НачалоМесяца(Контекст.КонецПериода);
	Контекст.ПредставлениеПериода = ПредставлениеПериода(
		Контекст.НачалоПериода, КонецМесяца(Контекст.КонецПериода), "ДФ='MMMM yyyy ""г.""'");
	
	СправкиРасчеты.ВывестиШапкуОтчета(Результат, Контекст, Истина);
	
КонецПроцедуры

Процедура ПослеВыводаРезультата(Контекст, Результат) Экспорт
	
	ВалютнаяОбластьУдалена = Ложь;
	
	// Удалим пустые группировки
	Индекс = Результат.ВысотаТаблицы;
	Пока Индекс > 0 Цикл
		УдаляемаяОбласть = Результат.Область("R" + Формат(Индекс, "ЧГ=0"));
		
		ЯчейкаУдалитьРуб = Результат.НайтиТекст("###УдалитьРуб###", УдаляемаяОбласть);
		УдалитьРуб = ЯчейкаУдалитьРуб <> Неопределено;
		УдалитьВал = Результат.НайтиТекст("###УдалитьВал###", УдаляемаяОбласть) <> Неопределено;
		
		Если УдалитьВал
			Или УдалитьРуб И Не ВалютнаяОбластьУдалена Тогда // если удалили пустую валютную область, то рублевую оставляем
			
			УдаляемыхСтрок = ?(УдалитьВал, 4, 3);
			
			Результат.УдалитьОбласть(УдаляемаяОбласть, ТипСмещенияТабличногоДокумента.ПоВертикали);
			Для Счетчик = 1 По УдаляемыхСтрок Цикл
				УдаляемаяОбластьВыше = Результат.Область("R" + Формат(Индекс - Счетчик, "ЧГ=0"));
				Результат.УдалитьОбласть(УдаляемаяОбластьВыше, ТипСмещенияТабличногоДокумента.ПоВертикали);
			КонецЦикла;
			
			Индекс = Индекс - УдаляемыхСтрок;
			
			ВалютнаяОбластьУдалена = Истина;
		ИначеЕсли УдалитьРуб И ВалютнаяОбластьУдалена Тогда
			ЯчейкаУдалитьРуб.Текст = ""; // очистим служебный комментарий
			Прервать;
		КонецЕсли;
		
		Индекс = Индекс - 1;
	КонецЦикла;
	
	СправкиРасчеты.ОформитьРезультатОтчета(Результат, Контекст);
	
	Результат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
КонецПроцедуры

Функция ПолучитьТекстЗаголовка(Контекст) Экспорт
	
	Возврат СправкиРасчеты.ЗаголовокОтчета(Контекст);
	
КонецФункции

Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	Манифест = Новый Структура;
	Манифест.Вставить("ИспользоватьДанныеРасшифровки",      Ложь);
	Манифест.Вставить("ИспользоватьПередКомпоновкойМакета", Истина);
	Манифест.Вставить("ИспользоватьПослеКомпоновкиМакета",  Ложь);
	Манифест.Вставить("ИспользоватьВнешниеНаборыДанных",    Ложь);
	Манифест.Вставить("ИспользоватьПриВыводеЗаголовка",     Истина);
	Манифест.Вставить("ИспользоватьПослеВыводаРезультата",  Истина);
	Манифест.Вставить("ИспользоватьНалоговыйПериод",        Ложь);
	
	СправкиРасчеты.УстановитьОтчетНеИспользуетНаборыСуммовыхПоказателей(Манифест);
	
	Возврат Манифест;
	
КонецФункции

#КонецОбласти

#КонецЕсли