#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИспользоватьПередКомпоновкойМакета", Истина);
	Результат.Вставить("ИспользоватьПослеВыводаРезультата",  Истина);
	Результат.Вставить("ИспользоватьДанныеРасшифровки",      Истина);
	Результат.Вставить("ИспользоватьПривилегированныйРежим", Ложь);

	Возврат Результат;

КонецФункции

Функция ПолучитьТекстЗаголовка(ПараметрыОтчета) Экспорт 
	
	Возврат "Сведения о контрагентах контролируемых сделок";
	
КонецФункции

// В процедуре можно доработать компоновщик перед выводом в отчет
// Изменения сохранены не будут
Процедура ПередКомпоновкойМакета(ПараметрыОтчета, Схема, КомпоновщикНастроек) Экспорт
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.ОтчетныйГод) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "Уведомление", ПараметрыОтчета.Уведомление);
	КонецЕсли;
	
	ОтчетныйГод = НачалоГода(Дата(ПараметрыОтчета.ОтчетныйГод,1,1));
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СписокОфшоров", КонтролируемыеСделки.СписокОфшоров(ОтчетныйГод));
	
	ЗаголовкиПолей = Новый Структура;
	Если ОтчетныйГод < КонтролируемыеСделки.ДатаОтказаОтЕНВД() Тогда
		ЗаголовкиПолей.Вставить("СделкаОблагаетсяЕНВД", НСтр("ru = 'Сделка облагается ЕНВД/ЕСХН'"));
	Иначе
		ЗаголовкиПолей.Вставить("СделкаОблагаетсяЕНВД", НСтр("ru = 'Сделка облагается ЕСХН'"));
	КонецЕсли;
	УстановитьЗаголовкиПолей(КомпоновщикНастроек.Настройки.Структура, ЗаголовкиПолей);
	
	// Дополнительные данные
	БухгалтерскиеОтчетыВызовСервера.ДобавитьДополнительныеПоля(ПараметрыОтчета, КомпоновщикНастроек);
	
КонецПроцедуры

Процедура ПослеВыводаРезультата(ПараметрыОтчета, Результат) Экспорт
	
	БухгалтерскиеОтчетыВызовСервера.ОбработкаРезультатаОтчета(ПараметрыОтчета.ИдентификаторОтчета, Результат);
	
	Если Результат.Области.Найти("Заголовок") = Неопределено Тогда
		Результат.ФиксацияСверху = 0;
	Иначе
		Результат.ФиксацияСверху = Результат.Области.Заголовок.Низ;
	КонецЕсли;
	
	Результат.ФиксацияСлева = 0;
	
КонецПроцедуры

Функция ПолучитьНаборПоказателей() Экспорт
	
	НаборПоказателей = Новый Массив;
	НаборПоказателей.Добавить("БУ");
	НаборПоказателей.Добавить("НУ");
	НаборПоказателей.Добавить("ПР");
	НаборПоказателей.Добавить("ВР");
	
	Возврат НаборПоказателей;
	
КонецФункции

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьЗаголовкиПолей(Структура, ЗаголовкиПолей)
	
	Если ТипЗнч(Структура) = Тип("КоллекцияЭлементовСтруктурыНастроекКомпоновкиДанных")
		Или ТипЗнч(Структура) = Тип("КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных") Тогда
		Для Каждого ЭлементСтруктуры Из Структура Цикл
			УстановитьЗаголовкиПолей(ЭлементСтруктуры, ЗаголовкиПолей);
		КонецЦикла;
	ИначеЕсли ТипЗнч(Структура) = Тип("ТаблицаКомпоновкиДанных") Тогда
		
		Если ЗначениеЗаполнено(Структура.Строки) Тогда
			УстановитьЗаголовкиПолей(Структура.Строки, ЗаголовкиПолей);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Структура) = Тип("ГруппировкаКомпоновкиДанных")
		Или ТипЗнч(Структура) = Тип("ГруппировкаТаблицыКомпоновкиДанных") Тогда
		
		УстановитьЗаголовкиПолейЭлементов(Структура.Выбор.Элементы, ЗаголовкиПолей);
		
		Если ЗначениеЗаполнено(Структура.Структура) Тогда
			УстановитьЗаголовкиПолей(Структура.Структура, ЗаголовкиПолей);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьЗаголовкиПолейЭлементов(ЭлементыНабораПолей, ЗаголовкиПолей)
	
	ИндексПоля = ЭлементыНабораПолей.Количество() - 1;
	Пока ИндексПоля >= 0 Цикл
		ПолеВыбора = ЭлементыНабораПолей[ИндексПоля];
		
		ИндексПоля = ИндексПоля - 1;
		
		Если ТипЗнч(ПолеВыбора) <> Тип("ПолеГруппировкиКомпоновкиДанных")
			И ТипЗнч(ПолеВыбора) <> Тип("ВыбранноеПолеКомпоновкиДанных") Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого КлючЗначение Из ЗаголовкиПолей Цикл
			Если ПолеВыбора.Поле = Новый ПолеКомпоновкиДанных(КлючЗначение.Ключ) Тогда
				ПолеВыбора.Заголовок = КлючЗначение.Значение;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли